SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF (OBJECT_ID('DBO.FN_CERT_LIST_4_REPEX_BILL_TORG12_NV_RIGLA') IS NULL)	EXEC ('CREATE FUNCTION DBO.FN_CERT_LIST_4_REPEX_BILL_TORG12_NV_RIGLA() RETURNS VARCHAR(4000) AS BEGIN RETURN CONVERT(VARCHAR(4000), NULL) END')
GO
ALTER FUNCTION DBO.FN_CERT_LIST_4_REPEX_BILL_TORG12_NV_RIGLA(
    @ID_SERIES BIGINT
)
RETURNS VARCHAR(4000)
AS
BEGIN
    DECLARE @RESULT VARCHAR(4000)
    SELECT 
        @RESULT = ISNULL(@RESULT + '; ' + CONVERT(NVARCHAR(12), C.CERT_DATE, 104) + ' выдан ' + C.ISSUED_BY, ISNULL(CONVERT(NVARCHAR(12), C.CERT_DATE, 104) + ' выдан ' + C.ISSUED_BY, ''))
    FROM CERTIFICATE C
    WHERE ID_SERIES = @ID_SERIES
    RETURN @RESULT
END
GO

IF OBJECT_ID('DBO.REPEX_BILL_TORG12_NV_RIGLA') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_BILL_TORG12_NV_RIGLA AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_BILL_TORG12_NV_RIGLA(
    @XMLPARAM NTEXT
) AS

DECLARE @HDOC INT 
DECLARE @ID_GLOBAL UNIQUEIDENTIFIER

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
SELECT TOP 1 
	@ID_GLOBAL = ID_GLOBAL
FROM OPENXML(@HDOC, '/XML') WITH(ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT	
	GOODS = G.NAME + ' [' + P.NAME + ', ' + COALESCE(C.NAME, '') + ']',
	GOODS_FEDERAL_CODE = G.FEDERAL_CODE,
	UNIT_NAME = U.NAME + '(' + CAST(SR.NUMERATOR AS VARCHAR) + '/' + CAST(SR.DENOMINATOR AS VARCHAR) + ')',
	OKEI_CODE = U.OKEI_CODE,
	QUANTITY = BI.QUANTITY,
	CONTRACTOR_PRICE_PER_UNIT = BI.PRICE_SAL - BI.PVAT_SAL,
	SUM_CONTRACTOR_PRICE = (BI.PRICE_SAL - BI.PVAT_SAL) * BI.QUANTITY,
	VAT_RATE = CASE WHEN BI.VAT_SAL = 0 THEN CAST('Без НДС' AS VARCHAR(10)) ELSE CAST(BI.VAT_SAL AS VARCHAR(10)) END,
	SUM_VAT = BI.PVAT_SAL * BI.QUANTITY,
	SUM_CONTRACTOR_PRICE_VAT = BI.PRICE_SAL * BI.QUANTITY,
    CERTIFICATION_INFO = DBO.FN_CERT_LIST_4_REPEX_BILL_TORG12_NV_RIGLA(S.ID_SERIES),
    S.BEST_BEFORE,
    S.SERIES_NUMBER
FROM BILL B
	INNER JOIN BILL_ITEM BI ON BI.ID_BILL_GLOBAL = B.ID_BILL_GLOBAL
	INNER JOIN GOODS G ON G.ID_GOODS = BI.ID_GOODS
	INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER	
	INNER JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
	LEFT JOIN LOT L ON L.ID_LOT = BI.ID_LOT
	LEFT JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
	LEFT JOIN UNIT U ON SR.ID_UNIT = U.ID_UNIT
	LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES
WHERE B.ID_BILL_GLOBAL = @ID_GLOBAL
ORDER BY GOODS

SELECT TOP 1
	INVOICE_NUMBER = B.DOC_NUM,
	INVICE_DATE = B.DOC_DATE,
	SUPPLIER_NAME = CASE WHEN ISNULL(SUP.FULL_NAME, '') != '' THEN SUP.FULL_NAME ELSE SUP.[NAME] END +
		COALESCE(' ИНН:' + SUP.INN, '') +
		COALESCE(' Адрес:' + SUP.ADDRESS, '') +
		COALESCE(' Телефон:' + SUP.PHONE, '') +
		COALESCE(' КПП:' + SUP.KPP, '') +
		COALESCE(' Банк:' + SUP.BANK + ' ' + SUP.BANK_ADDRESS , ' Банк:' + SUP.BANK, ' Банк:' + SUP.BANK_ADDRESS, '') +
		COALESCE(' р/c:' + SUP.ACCOUNT, '') +
		COALESCE(' к/с:' + SUP.CORR_ACCOUNT, '') +
		COALESCE(' БИК:' + SUP.BIK, ''),
	SUPPLIER_OKPO = SUP.OKPO,
	SUPPLIER_DESCRIPT =
		CASE WHEN ISNULL(SUP.FULL_NAME, '') != '' THEN SUP.FULL_NAME ELSE SUP.[NAME] END +
		COALESCE(' ИНН:' + SUP.INN, '') +
		COALESCE(' Адрес:' + SUP.ADDRESS, '') +
		COALESCE(' Телефон:' + SUP.PHONE, '') + 
		COALESCE(' КПП:' + SUP.KPP, '') +
		COALESCE(' Банк:' + SUP.BANK + ' ' + SUP.BANK_ADDRESS , ' Банк:' + SUP.BANK, ' Банк:' + SUP.BANK_ADDRESS, '') +
		COALESCE(' р/c:' + SUP.ACCOUNT, '') +
		COALESCE(' к/с:' + SUP.CORR_ACCOUNT, '') +
		COALESCE(' БИК:' + SUP.BIK, ''),
	CUSTOMER_NAME = COALESCE(NULLIF(RTRIM(LTRIM(CONT.FULL_NAME)),''), CONT.[NAME], ''),
	CUSTOMER_OKPO = CONT.OKPO,
	CUSTOMER_DESCRIPT = CASE WHEN ISNULL(CONT.FULL_NAME, '') != '' THEN CONT.FULL_NAME ELSE CONT.NAME END +
		COALESCE(' ИНН:' + CONT.INN, '') +
		COALESCE(' Адрес:' + CONT.ADDRESS, '') +
		COALESCE(' Телефон:' + CONT.PHONE, '') +
		COALESCE(' КПП:' + CONT.KPP, '') +
		COALESCE(' Банк:' + CONT.BANK + ' ' + CONT.BANK_ADDRESS, ' Банк:' + CONT.BANK, ' Банк:' + CONT.BANK_ADDRESS, '') +
		COALESCE(' р/c:' + CONT.ACCOUNT, '') +
		COALESCE(' к/с:' + CONT.CORR_ACCOUNT, '') +
		COALESCE(' БИК:' + CONT.BIK, ''), 
	PAYER_NAME = NULL,
	STORE_NAME = S.NAME,
	INCOMING_NUMBER = CR.NAME + ' от ' + CONVERT(VARCHAR, CR.START_DATE , 104)
FROM BILL B
	INNER JOIN STORE S ON S.ID_STORE = B.ID_STORE
	INNER JOIN CONTRACTOR SUP ON SUP.ID_CONTRACTOR = S.ID_CONTRACTOR
	INNER JOIN CONTRACTOR CONT ON CONT.ID_CONTRACTOR = B.ID_CONTRACTOR
	LEFT JOIN CONTRACTS CR ON CR.ID_CONTRACTS_GLOBAL = B.ID_CONTRACTS_GLOBAL
WHERE B.ID_BILL_GLOBAL = @ID_GLOBAL

RETURN 0
GO

--exec REPEX_BILL_TORG12_NV_RIGLA '<XML><ID_GLOBAL>2BE7D3F6-1193-4927-97BF-2D0925205CE6</ID_GLOBAL></XML>'