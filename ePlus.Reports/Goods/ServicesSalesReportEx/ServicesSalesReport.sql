SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_SERVICES_SALES_REPORT') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_SERVICES_SALES_REPORT AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_SERVICES_SALES_REPORT
	@XMLPARAM NTEXT AS
		
DECLARE	@HDOC INT
DECLARE @DATE_FROM DATETIME
DECLARE @DATE_TO DATETIME

DECLARE @ALL_SERVICES BIT
		
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
		
SELECT
	@DATE_FROM = DATE_FROM,
	@DATE_TO = DATE_TO
FROM OPENXML(@HDOC , '/XML') WITH(
	DATE_FROM DATETIME 'DATE_FROM',
	DATE_TO DATETIME 'DATE_TO'
)

SELECT * INTO #SERVICES FROM OPENXML(@HDOC, '/XML/ID_SERVICE') WITH(ID_SERVICE UNIQUEIDENTIFIER '.')
IF @@ROWCOUNT = 0 
	SET @ALL_SERVICES = 1
		
EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_DAYS	@DATE_FROM OUTPUT, @DATE_TO OUTPUT
EXEC DBO.USP_RANGE_NORM	@DATE_FROM OUTPUT, @DATE_TO OUTPUT

SELECT
	DOC_DATE = MAX(CH.DATE_CHEQUE),
	DOC_NUM = MAX(CS.MNEMOCODE),
	SUM_SAL = SUM(CASE WHEN CH.CHEQUE_TYPE = 'SALE' THEN 1 ELSE -1 END * CHI.QUANTITY * SI.PRICE_SAL * SI.QUANTITY)
FROM CHEQUE AS CH
	INNER JOIN CASH_SESSION AS CS ON CS.ID_CASH_SESSION_GLOBAL = CH.ID_CASH_SESSION_GLOBAL
	INNER JOIN CHEQUE_ITEM AS CHI ON CHI.ID_CHEQUE_GLOBAL = CH.ID_CHEQUE_GLOBAL
	INNER JOIN SERVICE_4_SALE AS S ON S.ID_SERVICE_4_SALE = CHI.ID_LOT_GLOBAL
	INNER JOIN SERVICE_4_SALE_ITEM SI ON SI.ID_SERVICE_4_SALE = S.ID_SERVICE_4_SALE
WHERE CH.DOCUMENT_STATE = 'PROC' and ch.cheque_type in ('SALE', 'RETURN')
	AND CH.DATE_CHEQUE BETWEEN @DATE_FROM AND @DATE_TO
	AND (@ALL_SERVICES = 1 OR SI.ID_SERVICE IN (SELECT ID_SERVICE FROM #SERVICES))
GROUP BY CS.ID_CASH_SESSION_GLOBAL
HAVING SUM(CASE WHEN CH.CHEQUE_TYPE = 'SALE' THEN 1 ELSE -1 END * CHI.QUANTITY * SI.PRICE_SAL * SI.QUANTITY) > 0
ORDER BY MAX(CH.DATE_CHEQUE)

SELECT SELF_NAME = NAME FROM CONTRACTOR WHERE ID_CONTRACTOR = DBO.FN_CONST_CONTRACTOR_SELF()

RETURN 0
GO

/*
exec REPEX_SERVICES_SALES_REPORT N'
<XML>
	<DATE_FROM>2009-07-29T17:23:28.031</DATE_FROM>
	<DATE_TO>2009-07-29T17:23:28.031</DATE_TO>
</XML>'*/