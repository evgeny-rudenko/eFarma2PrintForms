SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_INVOICE_REGISTRY') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_INVOICE_REGISTRY AS RETURN')
GO
ALTER  PROCEDURE DBO.REPEX_INVOICE_REGISTRY
    @XMLPARAM NTEXT AS
    
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME
DECLARE @DATE_TO DATETIME

DECLARE @ALL_STORE BIT
DECLARE @ALL_CONTRACTOR BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

SELECT
	@DATE_FR = DATE_FR,
	@DATE_TO = DATE_TO	
FROM OPENXML(@HDOC, '/XML') WITH(
	DATE_FR DATETIME 'DATE_FR',
	DATE_TO DATETIME 'DATE_TO'
)

SELECT * INTO #ID_STORES FROM OPENXML(@HDOC, '/XML/ID_STORES') WITH(ID_STORE BIGINT '.')
IF (@@ROWCOUNT = 0)	SET @ALL_STORE = 1

SELECT * INTO #ID_CONTRACTORS FROM OPENXML(@HDOC, '/XML/ID_CONTRACTORS') WITH(ID_CONTRACTOR BIGINT '.')
IF (@@ROWCOUNT = 0)	SET @ALL_CONTRACTOR = 1

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC USP_RANGE_DAYS @DATE_FR OUT, @DATE_TO OUT
EXEC USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

CREATE TABLE #VAT10 (
	ID_INVOICE BIGINT, 
	VAT_SUM MONEY
)

CREATE TABLE #VAT18 (
	ID_INVOICE BIGINT, 
	VAT_SUM MONEY
)

INSERT INTO #VAT10
        ( ID_INVOICE, VAT_SUM )
SELECT 
	II.ID_INVOICE, 
	SUM(II.SUPPLIER_VAT_SUM)
FROM INVOICE_ITEM II
INNER JOIN INVOICE I ON II.ID_INVOICE = I.ID_INVOICE
WHERE II.VAT = 10
	AND I.DOCUMENT_STATE = 'PROC'
	AND I.DOCUMENT_DATE BETWEEN @DATE_FR AND @DATE_TO
	AND (@ALL_CONTRACTOR = 1 OR I.ID_CONTRACTOR_SUPPLIER IN (SELECT ID_CONTRACTOR FROM #ID_CONTRACTORS))
	AND (@ALL_STORE = 1 OR I.ID_STORE IN (SELECT ID_STORE FROM #ID_STORES))
GROUP BY II.ID_INVOICE

CREATE INDEX I_VAT10 ON #VAT10(ID_INVOICE)

INSERT INTO #VAT18
        ( ID_INVOICE, VAT_SUM )
SELECT 
	II.ID_INVOICE, 
	SUM(II.SUPPLIER_VAT_SUM)
FROM INVOICE_ITEM II
INNER JOIN INVOICE I ON II.ID_INVOICE = I.ID_INVOICE
WHERE II.VAT = 18
	AND I.DOCUMENT_STATE = 'PROC'
	AND I.DOCUMENT_DATE BETWEEN @DATE_FR AND @DATE_TO
	AND (@ALL_CONTRACTOR = 1 OR I.ID_CONTRACTOR_SUPPLIER IN (SELECT ID_CONTRACTOR FROM #ID_CONTRACTORS))
	AND (@ALL_STORE = 1 OR I.ID_STORE IN (SELECT ID_STORE FROM #ID_STORES))
GROUP BY II.ID_INVOICE

CREATE INDEX I_VAT18 ON #VAT18(ID_INVOICE)

SELECT
	SU.ID_CONTRACTOR AS ID_SUPPLIER,  
	I.INCOMING_NUMBER, 
	SU.NAME AS SUPPLIER_NAME, 
	I.MNEMOCODE AS DOC_NUM, 
	I.SUM_SUPPLIER, 
	(ISNULL(V10.VAT_SUM, 0) + ISNULL(V18.VAT_SUM, 0)) AS VAT_SUM, 
	ISNULL(V10.VAT_SUM, 0) AS VAT10_SUM, 
	ISNULL(V18.VAT_SUM, 0) AS VAT18_SUM, 
	I.SUM_RETAIL
FROM INVOICE I
INNER JOIN CONTRACTOR SU ON I.ID_CONTRACTOR_SUPPLIER = SU.ID_CONTRACTOR
LEFT JOIN #VAT10 V10 ON V10.ID_INVOICE = I.ID_INVOICE
LEFT JOIN #VAT18 V18 ON V18.ID_INVOICE = I.ID_INVOICE
WHERE I.DOCUMENT_STATE = 'PROC'
AND I.DOCUMENT_DATE BETWEEN @DATE_FR AND @DATE_TO
AND (@ALL_CONTRACTOR = 1 OR I.ID_CONTRACTOR_SUPPLIER IN (SELECT ID_CONTRACTOR FROM #ID_CONTRACTORS))
AND (@ALL_STORE = 1 OR I.ID_STORE IN (SELECT ID_STORE FROM #ID_STORES))
ORDER BY SU.NAME, I.MNEMOCODE

DROP TABLE #VAT10
DROP TABLE #VAT18

RETURN 
GO
/*
exec REPEX_INVOICE_REGISTRY @XMLPARAM=N'<XML><DATE_FR>2010-01-01T00:00:00.000</DATE_FR>
<DATE_TO>2010-07-28T15:28:43.390</DATE_TO></XML>'
*/