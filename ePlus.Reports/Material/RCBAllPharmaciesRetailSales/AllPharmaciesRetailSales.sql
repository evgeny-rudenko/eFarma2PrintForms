SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_ALL_PHARMACIES_RETAIL_SALES') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_ALL_PHARMACIES_RETAIL_SALES AS RETURN')
GO
ALTER  PROCEDURE DBO.REPEX_ALL_PHARMACIES_RETAIL_SALES
    @XMLPARAM NTEXT AS
	DECLARE @HDOC INT
	DECLARE @DATE_FR DATETIME
	DECLARE @DATE_TO DATETIME	
	DECLARE @CO BIT
	DECLARE @BY_GROUPS BIT
	DECLARE @FROM_IO BIT
	
	DECLARE @ALL_STORES BIT
	DECLARE @ALL_GOODS BIT
	DECLARE @ALL_CONTRACTORS BIT
	DECLARE @ALL_GROUPS BIT
	
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

SELECT
	@DATE_FR = DATE_FR,
	@DATE_TO = DATE_TO,	
	@CO = CO, 
	@BY_GROUPS = BY_GROUPS, 
	@FROM_IO = FROM_IO
FROM OPENXML(@HDOC, '/XML') WITH(
	DATE_FR DATETIME 'DATE_FR',
	DATE_TO DATETIME 'DATE_TO', 
	CO BIT 'CO', 
	BY_GROUPS BIT 'BY_GROUPS', 
	FROM_IO BIT 'FROM_IO'
)

SELECT * INTO #ID_STORES FROM OPENXML(@HDOC, '/XML/ID_STORE') WITH(ID_STORE BIGINT '.')
IF (@@ROWCOUNT = 0)	SET @ALL_STORES = 1

SELECT * INTO #ID_GOODS FROM OPENXML(@HDOC, '/XML/ID_GOODS') WITH(ID_GOODS BIGINT '.')
IF (@@ROWCOUNT = 0)	SET @ALL_GOODS = 1

SELECT * INTO #ID_CONTRACTORS FROM OPENXML(@HDOC, '/XML/ID_CONTRACTOR') WITH(ID_CONTRACTOR BIGINT '.')
IF (@@ROWCOUNT = 0)	SET @ALL_CONTRACTORS = 1

SELECT * INTO #GUID_GROUPS FROM OPENXML(@HDOC, '/XML/GUID_GROUP') WITH(GUID_GROUP UNIQUEIDENTIFIER '.')
IF (@@ROWCOUNT = 0)	SET @ALL_GROUPS = 1

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC USP_RANGE_DAYS @DATE_FR OUT, @DATE_TO OUT
EXEC USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

CREATE TABLE #GOODS (
	ID_GOODS UNIQUEIDENTIFIER, 
	ID_GOODS_CLASSIFIER UNIQUEIDENTIFIER, 
	GROUP_NAME VARCHAR(100), 
	GOODS_NAME VARCHAR(400), 
	CODE VARCHAR(16), 
	ID_STORE BIGINT, 
	STORE_NAME VARCHAR(100), 
	ID_CONTRACTOR BIGINT, 
	DRUGSTORE VARCHAR(1000), 
	PAY_SUMM MONEY, 
	QUANTITY MONEY
)

IF @FROM_IO <> 1
BEGIN
	INSERT INTO #GOODS
        ( ID_GOODS ,
          ID_GOODS_CLASSIFIER ,
		  GROUP_NAME ,
          GOODS_NAME ,
          CODE ,
          ID_STORE ,
          STORE_NAME ,
          ID_CONTRACTOR ,
          DRUGSTORE ,
          PAY_SUMM ,
          QUANTITY
        )
	SELECT 
		G.ID_GOODS_GLOBAL, 
		GC2G.ID_GOODS_CLASSIFIER, 
		GC2G.GROUP_NAME, 
		G.NAME + CASE WHEN ISNULL(P.NAME, '') = '' THEN '' ELSE ' (' + P.NAME + ')' END, 
		G.CODE, 
		S.ID_STORE, 
		S.NAME, 
		C.ID_CONTRACTOR, 
		ISNULL(C.FULL_NAME, C.NAME), 
		SUM(CHI.SUMM * CASE WHEN CH.CHEQUE_TYPE = 'SALE' THEN 1 ELSE -1 END), 
		SUM(CHI.QUANTITY * CASE WHEN CH.CHEQUE_TYPE = 'SALE' THEN 1 ELSE -1 END * 
			CASE WHEN SR.ID_SCALING_RATIO IS NULL THEN 1 ELSE CONVERT(MONEY, SR.NUMERATOR) / CONVERT(MONEY,SR.DENOMINATOR) END/**/)
	--INTO #GOODS
	FROM CHEQUE_ITEM CHI
	INNER JOIN CHEQUE CH ON CHI.ID_CHEQUE_GLOBAL = CH.ID_CHEQUE_GLOBAL
	INNER JOIN GOODS G ON CHI.ID_GOODS = G.ID_GOODS
	INNER JOIN LOT L ON CHI.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
	INNER JOIN STORE S ON L.ID_STORE = S.ID_STORE
	INNER JOIN CONTRACTOR C ON S.ID_CONTRACTOR = C.ID_CONTRACTOR
	LEFT JOIN SCALING_RATIO SR ON CHI.ID_SCALING_RATIO = SR.ID_SCALING_RATIO
	LEFT JOIN PRODUCER P ON G.ID_PRODUCER = P.ID_PRODUCER
	LEFT JOIN (
		SELECT 
			GC2G.ID_GOODS, 
			GC2G.ID_GOODS_CLASSIFIER, 
			GROUP_NAME = GC.NAME
		FROM GOODS_CLASSIFIER_2_GOODS GC2G
		INNER JOIN GOODS_CLASSIFIER GC ON GC2G.ID_GOODS_CLASSIFIER = GC.ID_GOODS_CLASSIFIER
		WHERE GC.DATE_DELETED IS NULL AND GC2G.DATE_DELETED IS NULL
	) GC2G ON GC2G.ID_GOODS = G.ID_GOODS_GLOBAL

	WHERE CH.DOCUMENT_STATE = 'PROC' AND CH.CHEQUE_TYPE IN ('SALE', 'RETURN')
	-- FILTERS
	AND CH.DATE_CHEQUE BETWEEN @DATE_FR AND @DATE_TO
	AND ((@CO = 1 AND (@ALL_CONTRACTORS = 1 OR C.ID_CONTRACTOR IN (SELECT ID_CONTRACTOR FROM #ID_CONTRACTORS)))
		OR ((@CO <> 1 OR @CO IS NULL) AND C.ID_CONTRACTOR = (SELECT TOP 1 ID_CONTRACTOR FROM ENTERPRISE_BRANCH WHERE IS_SELF = 1)))
	AND (@ALL_STORES = 1 OR S.ID_STORE IN (SELECT ID_STORE FROM #ID_STORES))
	AND (@ALL_GOODS = 1 OR G.ID_GOODS IN (SELECT ID_GOODS FROM #ID_GOODS))
		
	GROUP BY G.ID_GOODS_GLOBAL, G.NAME, P.NAME, G.CODE, C.ID_CONTRACTOR, S.ID_STORE, S.NAME, 
		C.FULL_NAME, C.NAME, GC2G.ID_GOODS_CLASSIFIER, GC2G.GROUP_NAME
END
ELSE
BEGIN
	INSERT INTO #GOODS
        ( ID_GOODS ,
          ID_GOODS_CLASSIFIER ,
		  GROUP_NAME ,
          GOODS_NAME ,
          CODE ,
          ID_STORE ,
          STORE_NAME ,
          ID_CONTRACTOR ,
          DRUGSTORE ,
          PAY_SUMM ,
          QUANTITY
        )
	SELECT 
		G.ID_GOODS_GLOBAL, 
		GC2G.ID_GOODS_CLASSIFIER, 
		GC2G.GROUP_NAME, 
		G.NAME + CASE WHEN ISNULL(P.NAME, '') = '' THEN '' ELSE ' (' + P.NAME + ')' END, 
		G.CODE, 
		S.ID_STORE, 
		S.NAME,
		C.ID_CONTRACTOR, 
		ISNULL(C.FULL_NAME, C.NAME), 
		SUM(II.SUM_SAL - ISNULL(RT.PAY_SUMM, 0)), 
		SUM(II.QUANTITY * CASE WHEN SR.ID_SCALING_RATIO IS NULL THEN 1 ELSE CONVERT(MONEY, SR.NUMERATOR) / CONVERT(MONEY,SR.DENOMINATOR) END - 
			ISNULL(RT.QUANTITY, 0))
	--INTO #GOODS
	FROM INVOICE_OUT_ITEM II
	INNER JOIN INVOICE_OUT I ON II.ID_INVOICE_OUT_GLOBAL = I.ID_INVOICE_OUT_GLOBAL
	INNER JOIN LOT L ON II.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
	INNER JOIN GOODS G ON L.ID_GOODS = G.ID_GOODS
	INNER JOIN STORE S ON L.ID_STORE = S.ID_STORE
	INNER JOIN CONTRACTOR C ON S.ID_CONTRACTOR = C.ID_CONTRACTOR
	--ÑÂßÇÛÂÀÅÌ Ñ ÂÎÇÂÐÀÒÀÌÈ
	LEFT JOIN
	(
		SELECT 
			AR.ID_DOCUMENT_BASE, 
			ARI.ID_GOODS, 
			PAY_SUMM = ISNULL(SUM(ARI.PRICE_PER_UNIT * ARI.QUANTITY), 0), 
			QUANTITY = ISNULL(SUM(ARI.QUANTITY * CASE WHEN SRA.ID_SCALING_RATIO IS NULL THEN 1 ELSE CONVERT(MONEY, SRA.NUMERATOR) / CONVERT(MONEY,SRA.DENOMINATOR) END), 0)
		FROM ACT_RETURN_TO_BUYER AR 
		INNER JOIN ACT_RETURN_TO_BUYER_ITEM ARI ON AR.ID_ACT_RETURN_TO_BUYER = ARI.ID_ACT_RETURN_TO_BUYER
		LEFT JOIN SCALING_RATIO SRA ON ARI.ID_SCALING_RATIO = SRA.ID_SCALING_RATIO
		WHERE AR.ID_TABLE_DATA_DOCUMENT_BASE = 21 AND AR.DOCUMENT_STATE = 'PROC'
			AND AR.DATE BETWEEN @DATE_FR AND @DATE_TO
		GROUP BY AR.ID_DOCUMENT_BASE, ARI.ID_GOODS
	) RT ON RT.ID_DOCUMENT_BASE = I.ID_INVOICE_OUT AND RT.ID_GOODS = G.ID_GOODS

	LEFT JOIN SCALING_RATIO SR ON L.ID_SCALING_RATIO = SR.ID_SCALING_RATIO
	LEFT JOIN PRODUCER P ON G.ID_PRODUCER = P.ID_PRODUCER
	LEFT JOIN (
		SELECT 
			GC2G.ID_GOODS, 
			GC2G.ID_GOODS_CLASSIFIER, 
			GROUP_NAME = GC.NAME
		FROM GOODS_CLASSIFIER_2_GOODS GC2G
		INNER JOIN GOODS_CLASSIFIER GC ON GC2G.ID_GOODS_CLASSIFIER = GC.ID_GOODS_CLASSIFIER
		WHERE GC.DATE_DELETED IS NULL AND GC2G.DATE_DELETED IS NULL
	) GC2G ON GC2G.ID_GOODS = G.ID_GOODS_GLOBAL
	WHERE I.STATE = 'PROC'
	-- FILTERS
	AND I.[DATE] BETWEEN @DATE_FR AND @DATE_TO
	AND ((@CO = 1 AND (@ALL_CONTRACTORS = 1 OR C.ID_CONTRACTOR IN (SELECT ID_CONTRACTOR FROM #ID_CONTRACTORS)))
		OR ((@CO <> 1 OR @CO IS NULL) AND C.ID_CONTRACTOR = (SELECT TOP 1 ID_CONTRACTOR FROM ENTERPRISE_BRANCH WHERE IS_SELF = 1)))
	AND (@ALL_STORES = 1 OR S.ID_STORE IN (SELECT ID_STORE FROM #ID_STORES))
	AND (@ALL_GOODS = 1 OR G.ID_GOODS IN (SELECT ID_GOODS FROM #ID_GOODS))
	
	GROUP BY G.ID_GOODS_GLOBAL, G.NAME, P.NAME, G.CODE, C.ID_CONTRACTOR, S.ID_STORE, S.NAME, 
		C.FULL_NAME, C.NAME, GC2G.ID_GOODS_CLASSIFIER, GC2G.GROUP_NAME
END

IF @BY_GROUPS = 1
BEGIN
	IF @ALL_GROUPS = 1
	BEGIN
		SELECT * FROM
		(
		SELECT 
			CODE = LEFT(CONVERT(VARCHAR(19), MIN(CONVERT(MONEY, G.CODE))), LEN(CONVERT(VARCHAR(19), MIN(CONVERT(MONEY, G.CODE))))-3), 
			NAME = G.GROUP_NAME + ' (ãðóïïà)', 
			G.ID_CONTRACTOR, 
			G.DRUGSTORE, 
			G.ID_STORE, 
			G.STORE_NAME, 
			ID = G.ID_GOODS_CLASSIFIER, 
			PAY_SUMM = SUM(G.PAY_SUMM), 
			QUANTITY = SUM(G.QUANTITY)
		FROM #GOODS G
		WHERE G.ID_GOODS_CLASSIFIER IS NOT NULL
		GROUP BY G.ID_GOODS_CLASSIFIER, G.GROUP_NAME, G.ID_CONTRACTOR, G.DRUGSTORE, G.ID_STORE, G.STORE_NAME
		UNION ALL
		SELECT 
			G.CODE, 
			G.GOODS_NAME, 
			G.ID_CONTRACTOR, 
			G.DRUGSTORE, 
			G.ID_STORE, 
			G.STORE_NAME, 
			G.ID_GOODS, 
			G.PAY_SUMM, 
			G.QUANTITY
		FROM #GOODS G
		WHERE G.ID_GOODS_CLASSIFIER IS NULL
		) A
		ORDER BY A.NAME
	END
	ELSE
	BEGIN
		SELECT 
			CODE = LEFT(CONVERT(VARCHAR(19), MIN(CONVERT(MONEY, G.CODE))), LEN(CONVERT(VARCHAR(19), MIN(CONVERT(MONEY, G.CODE))))-3), 
			NAME = G.GROUP_NAME + ' (ãðóïïà)', 
			G.ID_CONTRACTOR, 
			G.DRUGSTORE, 
			G.ID_STORE, 
			G.STORE_NAME, 
			ID = G.ID_GOODS_CLASSIFIER, 
			PAY_SUMM = SUM(G.PAY_SUMM), 
			QUANTITY = SUM(G.QUANTITY)
		FROM #GOODS G
		WHERE G.ID_GOODS_CLASSIFIER IS NOT NULL
			AND G.ID_GOODS_CLASSIFIER IN (SELECT GUID_GROUP FROM #GUID_GROUPS)
		GROUP BY G.ID_GOODS_CLASSIFIER, G.GROUP_NAME, G.ID_CONTRACTOR, G.DRUGSTORE, G.ID_STORE, G.STORE_NAME
		ORDER BY G.GROUP_NAME + ' (ãðóïïà)'
	END
END
ELSE
BEGIN
	SELECT 
		G.CODE, 
		NAME = G.GOODS_NAME, 
		G.ID_CONTRACTOR, 
		G.DRUGSTORE, 
		G.ID_STORE, 
		G.STORE_NAME, 
		ID = G.ID_GOODS, 
		G.PAY_SUMM, 
		G.QUANTITY
	FROM #GOODS G
	ORDER BY G.GOODS_NAME
END

RETURN
GO

/*
exec REPEX_ALL_PHARMACIES_RETAIL_SALES @xmlParam=N'<XML><DATE_FR>2000-10-26T11:05:35.265</DATE_FR><DATE_TO>2010-11-08T11:05:35.265</DATE_TO><BY_GROUPS>0</BY_GROUPS><FROM_IO>0</FROM_IO><CO>1</CO></XML>'
*/