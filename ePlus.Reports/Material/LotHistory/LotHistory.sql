SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_LOT_HISTORY') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_LOT_HISTORY AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_LOT_HISTORY
    @XMLPARAM NTEXT AS

DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME
DECLARE @DATE_TO DATETIME
DECLARE @ID_DEFECT BIGINT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT

SELECT
	@DATE_FR = DATE_FR,
	@DATE_TO = DATE_TO,
	@ID_DEFECT = ID_DEFECT
FROM OPENXML(@HDOC, '/XML') WITH(
	DATE_FR DATETIME 'DATE_FR',
	DATE_TO DATETIME 'DATE_TO',
	ID_DEFECT BIGINT 'ID_DEFECT')

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT
EXEC USP_RANGE_DAYS @DATE_FR OUT, @DATE_TO OUT

SELECT
	GOODS_NAME = G.NAME,
	LOT = L.LOT_NAME, 
	SERIES = S.SERIES_NUMBER,
	BEST_BEFORE = S.BEST_BEFORE,
	DEF_DOC_NUM = D.MNEMOCODE,
	UNIT_NAME = CAST(SR.NUMERATOR AS VARCHAR(5)) + '/' + CAST(SR.DENOMINATOR AS VARCHAR(5)) + ' ' + U.NAME,
	QUANTITY = DD.QUANTITY_REM,
	PRICE_PROD = L.PRICE_PROD,
	PRICE_SUP = L.PRICE_SUP,
	PRICE_SAL = L.PRICE_SAL,
	SUM_PROD = DD.QUANTITY_REM * L.PRICE_PROD,
	SUM_SUP = DD.QUANTITY_REM * L.PRICE_SUP,
	SUM_SAL = DD.QUANTITY_REM * L.PRICE_SAL,
	SUPPLIER_NAME = SUP.NAME
FROM DEFECT_JOURNAL_DETAIL DD
	INNER JOIN DEFECT_JOURNAL D ON D.ID_DEFECT_JOURNAL = DD.ID_DEFECT_JOURNAL
	INNER JOIN LOT L ON L.ID_LOT = DD.ID_LOT
	INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	INNER JOIN CONTRACTOR SUP ON SUP.ID_CONTRACTOR = L.ID_SUPPLIER
	LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES	
WHERE ID_DEFECT_JOURNAL_DETAIL = @ID_DEFECT

SELECT
	DOC_NAME = TD.DESCRIPTION,
	MNEMOCODE = AD.DOC_NUM,
	DOC_DATE = AD.DOC_DATE,
	STORE_NAME = S.NAME,
	UNIT_NAME = CAST(SR.NUMERATOR AS VARCHAR(5)) + '/' + CAST(SR.DENOMINATOR AS VARCHAR(5)) + ' ' + U.NAME,
	QUANTITY_ADD = LM.QUANTITY_ADD,
	QUANTITY_SUB = LM.QUANTITY_SUB,
	QUANTITY_RES = LM.QUANTITY_RES,
	CONTRACTOR = CASE WHEN TD.ID_TABLE_DATA = 8 THEN CASE WHEN LM.QUANTITY_SUB > 0 THEN C_TO.NAME WHEN LM.QUANTITY_ADD > 0 THEN C_FR.NAME END
					WHEN TD.ID_TABLE_DATA IN (2, 20, 6, 13, 24, 12, 23, 29, 37, 39, 38) THEN C_FR.NAME
					WHEN TD.ID_TABLE_DATA IN (21, 3) THEN C_TO.NAME
					WHEN TD.ID_TABLE_DATA = 19 THEN NULL
					WHEN TD.ID_TABLE_DATA = 22 THEN BILL_C.NAME
					WHEN TD.ID_TABLE_DATA = 30 THEN IRI_C.NAME
				ELSE NULL END
FROM LOT_MOVEMENT LM
	INNER JOIN LOT L ON L.ID_LOT_GLOBAL = LM.ID_LOT_GLOBAL
	INNER JOIN DEFECT_JOURNAL_DETAIL DD ON DD.ID_LOT = L.ID_LOT
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	INNER JOIN STORE S ON L.ID_STORE = S.ID_STORE
	INNER JOIN TABLE_DATA TD ON TD.ID_TABLE_DATA = LM.ID_TABLE
	LEFT JOIN ALL_DOCUMENT AD ON LM.ID_DOCUMENT = AD.ID_DOCUMENT_GLOBAL
	LEFT JOIN CONTRACTOR C_FR ON C_FR.ID_CONTRACTOR = AD.ID_CONTRACTOR_FROM
	LEFT JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = AD.ID_CONTRACTOR_TO

	LEFT JOIN BILL ON BILL.ID_BILL_GLOBAL = LM.ID_DOCUMENT
	LEFT JOIN CONTRACTOR BILL_C ON BILL_C.ID_CONTRACTOR = BILL.ID_CONTRACTOR

	LEFT JOIN IMPORT_REMAINS_ITEM IRI ON IRI.ID_IMPORT_REMAINS_ITEM_GLOBAL = LM.ID_DOCUMENT_ITEM
	LEFT JOIN CONTRACTOR IRI_C ON IRI_C.ID_CONTRACTOR = IRI.ID_SUPPLIER

WHERE LM.DATE_OP BETWEEN @DATE_FR AND @DATE_TO AND DD.ID_DEFECT_JOURNAL_DETAIL = @ID_DEFECT

RETURN 0
GO


/*
exec REPEX_LOT_HISTORY N'
<XML>
	<DATE_FR>2008-01-01T00:00:00.000</DATE_FR>
	<DATE_TO>2010-01-31T00:00:00.000</DATE_TO>
	<ID_DEFECT>28209</ID_DEFECT>
</XML>'*/