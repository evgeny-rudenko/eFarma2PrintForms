SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_SALEADPRICE_ROSA') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_SALEADPRICE_ROSA AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_SALEADPRICE_ROSA
    @XMLPARAM NTEXT
AS

DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME
DECLARE @ALL_STORE BIT, @ALL_GOODS_KIND BIT, @ALL_GOODS BIT, @SHOW_GOODS BIT

DECLARE @HDOC INT


EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT

SELECT
	@DATE_FR = DATE_FR,
	@DATE_TO = DATE_TO,
	@SHOW_GOODS = SHOW_GOODS
FROM OPENXML(@HDOC, '/XML') WITH(
	DATE_FR DATETIME 'DATE_FR',
	DATE_TO DATETIME 'DATE_TO',
	SHOW_GOODS BIT 'GOODS_DETAIL'
)

SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') WITH(ID_STORE BIGINT '.')
IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_GOODS = 0

SELECT * INTO #GOODS FROM OPENXML(@HDOC, '//ID_GOODS') WITH(ID_GOODS BIGINT '.')
IF @@ROWCOUNT = 0 SET @ALL_GOODS = 1 ELSE SET @ALL_GOODS = 0

SELECT * INTO #GOODS_KIND FROM OPENXML(@HDOC, '//ID_GOODS_KIND') WITH(ID_GOODS_KIND BIGINT '.')
IF @@ROWCOUNT = 0 SET @ALL_GOODS_KIND = 1 ELSE SET @ALL_GOODS_KIND = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT
EXEC DBO.REP_RANGEDAY @DATE_FR OUT, @DATE_TO OUT

SELECT 
	GROUP_NAME = ISNULL(GK.NAME, 'Вид не задан')
	, ID_GK = G.ID_GOODS_KIND
	, GOODS_NAME = G.NAME
	, ID_GOODS = G.ID_GOODS
	, PRICE_SUP = SUM(L.PRICE_SUP * DI.QUANTITY * SR.NUMERATOR / SR.DENOMINATOR)
	, PRICE_SAL = SUM(L.PRICE_SAL * DI.QUANTITY * SR.NUMERATOR / SR.DENOMINATOR)
	, DELTA = SUM((L.PRICE_SAL - L.PRICE_SUP) * DI.QUANTITY * SR.NUMERATOR / SR.DENOMINATOR)
	, ADPRICE = ROUND(SUM(L.PRICE_SAL - L.PRICE_SUP) / SUM(L.PRICE_SUP), 2) * 100
INTO #TAB
FROM CHEQUE D
	JOIN CHEQUE_ITEM DI ON DI.ID_CHEQUE_GLOBAL = D.ID_CHEQUE_GLOBAL
	JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = DI.ID_SCALING_RATIO
	JOIN LOT L ON L.ID_LOT_GLOBAL = DI.ID_LOT_GLOBAL
	JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	LEFT JOIN GOODS_KIND GK ON GK.ID_GOODS_KIND = G.ID_GOODS_KIND
WHERE 1=1
	AND D.DOCUMENT_STATE = 'PROC'
	AND D.DATE_CHEQUE BETWEEN @DATE_FR AND @DATE_TO
	AND (@ALL_STORE = 1 OR L.ID_STORE IN (SELECT ID_STORE FROM #STORE))
	AND (@ALL_GOODS = 1 OR L.ID_GOODS IN (SELECT ID_GOODS FROM #GOODS))
	AND (@ALL_GOODS_KIND = 1 OR G.ID_GOODS_KIND IN (SELECT ID_GOODS_KIND FROM #GOODS_KIND))
GROUP BY
	GK.NAME
	, G.ID_GOODS_KIND
	, G.NAME
	, G.ID_GOODS

IF @SHOW_GOODS = 1
BEGIN
	SELECT * FROM #TAB
END
ELSE
BEGIN
	SELECT 
		GROUP_NAME
		, ID_GK
		, PRICE_SUP = SUM(PRICE_SUP)
		, PRICE_SAL = SUM(PRICE_SAL)
		, DELTA = SUM(DELTA)
		, ADPRICE = AVG(ADPRICE)
	FROM #TAB
	GROUP BY
		GROUP_NAME
		, ID_GK
END

RETURN 0
GO
/*
EXEC DBO.REPEX_SALEADPRICE_ROSA N'
<XML>
  <DATE_FR>2011-04-01T14:42:02.053</DATE_FR>
  <DATE_TO>2011-04-10T14:42:02.053</DATE_TO>
  <ID_STORE>162</ID_STORE>
  <GOODS_DETAIL>0</GOODS_DETAIL>
</XML>
'
*/