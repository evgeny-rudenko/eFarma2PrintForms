SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('REPEX_ACCOUNT_DATARESERVE') IS NULL EXEC('CREATE PROCEDURE REPEX_ACCOUNT_DATARESERVE AS RETURN')
GO
ALTER PROCEDURE REPEX_ACCOUNT_DATARESERVE
	@XMLPARAM NTEXT AS

DECLARE @HDOC INT
DECLARE @TEXT NVARCHAR(4000),
	@DATE_FR DATETIME,
	@DATE_TO DATETIME,
	@DATAFILTER NVARCHAR(500),
    @ALL_CONTRACTOR BIT,
	@ALL_STORE BIT,
	@ALL_INVOICE BIT

DECLARE @QUERY NVARCHAR(4000)

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
SELECT 
	@DATE_FR = DATE_FR,
	@DATE_TO = DATE_TO
FROM OPENXML(@HDOC, '/XML') 
WITH(DATE_FR DATETIME 'DATE_FR',
	DATE_TO DATETIME 'DATE_TO')

SELECT * INTO #CONTRACTOR FROM OPENXML(@HDOC, '//ID_CONTRACTOR') WITH(ID_CONTRACTOR BIGINT '.')
IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR = 1

SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
WITH(ID_STORE BIGINT '.') IF @@ROWCOUNT = 0 SET @ALL_STORE = 1

SELECT @DATAFILTER = DATAFILTER FROM OPENXML(@HDOC, '//DATAFILTER') 
WITH(DATAFILTER VARCHAR(500) '.')

EXEC SP_XML_REMOVEDOCUMENT @HDOC	

--SELECT * FROM #CONTRACTOR	
--SELECT * FROM #STORE
--SELECT @DATAFILTER

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT
EXEC DBO.REP_RANGEDAY @DATE_FR OUT, @DATE_TO OUT

--SELECT @DATE_FR, @DATE_TO

SET @QUERY = N'
SELECT 
	DOC_NUM = B.DOC_NUM,
	END_RESERVATION_DATE = B.END_RESERVATION_DATE,
	NO_OF_DAYS = DATEDIFF(DAY, GETDATE(), B.END_RESERVATION_DATE)
FROM BILL B
	INNER JOIN BILL_ITEM BI ON BI.ID_BILL_GLOBAL = B.ID_BILL_GLOBAL
WHERE B.DOC_STATE = ''PROC''
	AND B.DOC_DATE BETWEEN @DATE_FR AND @DATE_TO
	AND (@ALL_CONTRACTOR = 1 OR B.ID_CONTRACTOR IN (SELECT ID_CONTRACTOR FROM #CONTRACTOR))
	AND (@ALL_STORE = 1 OR B.ID_STORE IN (SELECT ID_STORE FROM #STORE))
	AND (ISNULL((SELECT SUM(QUANTITY) FROM BILL_ITEM WHERE ID_BILL_GLOBAL = B.ID_BILL_GLOBAL AND ID_GOODS = BI.ID_GOODS), 0)
		- ISNULL((SELECT SUM(II.QUANTITY) FROM INVOICE_OUT I
										INNER JOIN INVOICE_OUT_ITEM II ON I.ID_INVOICE_OUT_GLOBAL = II.ID_INVOICE_OUT_GLOBAL
										INNER JOIN LOT L ON II.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
									WHERE ID_DOC_BASE_GLOBAL = B.ID_BILL_GLOBAL AND L.ID_GOODS = BI.ID_GOODS AND L.ID_STORE = B.ID_STORE), 0)) > 0'

IF (@DATAFILTER <> '')
	SET @QUERY = @QUERY + N' AND CAST(CONVERT(VARCHAR(8), B.END_RESERVATION_DATE, 112) AS DATETIME) ' + @DATAFILTER

SET @QUERY = @QUERY + N' GROUP BY B.DOC_NUM, B.END_RESERVATION_DATE, DATEDIFF(DAY, GETDATE(), B.END_RESERVATION_DATE) ORDER BY DATEDIFF(DAY, GETDATE(), B.END_RESERVATION_DATE), B.DOC_NUM'

PRINT @QUERY

EXEC SP_EXECUTESQL @QUERY, N'@ALL_STORE BIT, @ALL_CONTRACTOR BIT, @DATE_FR DATETIME, @DATE_TO DATETIME', 
	@ALL_STORE = @ALL_STORE, @ALL_CONTRACTOR = @ALL_CONTRACTOR, @DATE_FR = @DATE_FR, @DATE_TO = @DATE_TO

RETURN 0
GO

/*
EXEC REPEX_ACCOUNT_DATARESERVE N'
<XML>
	<DATE_FR>2009-08-06T10:46:40.015</DATE_FR>
	<DATE_TO>2009-08-06T10:46:40.015</DATE_TO>
	<DATAFILTER> > ''20090805''</DATAFILTER>
	<ID_CONTRACTOR>5282</ID_CONTRACTOR>
</XML>'*/

