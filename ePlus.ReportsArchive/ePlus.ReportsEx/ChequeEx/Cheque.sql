SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('REPEX_CHEQUE') IS NULL EXEC('CREATE PROCEDURE REPEX_CHEQUE AS RETURN')
GO

ALTER PROCEDURE REPEX_CHEQUE
	(@XMLPARAM NTEXT) AS

/* PARAMETERS */
DECLARE @HDOC INT
DECLARE @STORE VARCHAR(500)
DECLARE @LEN INT
DECLARE @ID_CHEQUE_GLOBAL UNIQUEIDENTIFIER

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
	SELECT TOP 1 @ID_CHEQUE_GLOBAL = ID_CHEQUE_GLOBAL
	FROM OPENXML(@HDOC, '/XML') 
	WITH(ID_CHEQUE_GLOBAL UNIQUEIDENTIFIER 'ID_CHEQUE_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC


SET @STORE = ''
SET @LEN = 0
SELECT @STORE = @STORE + ST.[NAME] + ', ' FROM
	(SELECT DISTINCT S.ID_STORE, S.[NAME] 
		FROM CHEQUE CH
			INNER JOIN CHEQUE_ITEM CH_I ON CH_I.ID_CHEQUE_GLOBAL = CH.ID_CHEQUE_GLOBAL
			INNER JOIN LOT L ON CH_I.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
			INNER JOIN STORE S ON L.ID_STORE = S.ID_STORE
		WHERE CH.ID_CHEQUE_GLOBAL = @ID_CHEQUE_GLOBAL) ST

SET @LEN = LEN(@STORE)
IF (@LEN >= 1)
BEGIN
  SET @STORE = LEFT(@STORE, @LEN - 1)
END

SELECT
	CAST(CH.ID_CHEQUE AS VARCHAR) AS N_CHEQUE,CH.DATE_CHEQUE,
 	CONTRACTOR_NAME = case 
 	                      when C.FULL_NAME is null then C.NAME
 	                      when C.FULL_NAME = '' then C.NAME
 	                      else C.FULL_NAME
 	                  end,    
 	C.ADDRESS, C.INN,
	@STORE AS STORE,
	(SELECT SUM(SUMM) FROM CHEQUE_ITEM WHERE ID_CHEQUE_GLOBAL = @ID_CHEQUE_GLOBAL) AS ITOGO
FROM CHEQUE CH
	INNER JOIN CASH_SESSION CS ON CH.ID_CASH_SESSION_GLOBAL = CS.ID_CASH_SESSION_GLOBAL
	LEFT JOIN CASH_REGISTER CR ON CS.ID_CASH_REGISTER = CR.ID_CASH_REGISTER
	LEFT JOIN CONTRACTOR C ON CR.ID_CONTRACTOR = C.ID_CONTRACTOR
WHERE CH.ID_CHEQUE_GLOBAL = @ID_CHEQUE_GLOBAL 

SELECT 
	G.[NAME] AS GOODS_NAME,
	P.[NAME] AS PRODUCER_NAME,
	CH_I.QUANTITY,
	U.SHORT_NAME AS UNIT_NAME,
	CH_I.PRICE,
	CH_I.SUMM_DISCOUNT,
	(CH_I.QUANTITY * CH_I.PRICE) AS SUMM,
	CH_I.SUMM AS SUMM_ITOGO 	
FROM CHEQUE CH
	INNER JOIN CHEQUE_ITEM CH_I ON CH.ID_CHEQUE_GLOBAL = CH_I.ID_CHEQUE_GLOBAL
	INNER JOIN GOODS G ON CH_I.ID_GOODS = G.ID_GOODS
	INNER JOIN SCALING_RATIO SR ON CH_I.ID_SCALING_RATIO = SR.ID_SCALING_RATIO
	LEFT JOIN PRODUCER P ON G.ID_PRODUCER = P.ID_PRODUCER
	LEFT JOIN UNIT U ON SR.ID_UNIT = U.ID_UNIT
WHERE CH.ID_CHEQUE_GLOBAL = @ID_CHEQUE_GLOBAL
ORDER BY GOODS_NAME

RETURN 0
GO