SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('REPEX_ACT_RETURN_TORG2') IS NULL EXEC('CREATE PROCEDURE REPEX_ACT_RETURN_TORG2 AS RETURN')
GO

ALTER PROCEDURE REPEX_ACT_RETURN_TORG2
	(@XMLPARAM NTEXT)
AS

DECLARE @HDOC INT
DECLARE @ID_DOCUMENT BIGINT
DECLARE @ID_DOCUMENT_BASE BIGINT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
	SELECT TOP 1
		@ID_DOCUMENT = ID_DOCUMENT
	FROM OPENXML(@HDOC, '/XML') 
	WITH (ID_DOCUMENT BIGINT 'ID_DOCUMENT')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT	@ID_DOCUMENT_BASE = AR.ID_DOCUMENT_BASE
FROM	DBO.ACT_RETURN_TO_CONTRACTOR AR
WHERE	AR.ID_ACT_RETURN_TO_CONTRACTOR = @ID_DOCUMENT

SELECT	AR.MNEMOCODE,
	AR.DATE,
    CONTRACTOR_FROM_NAME = case
	                         when CR_FROM.FUll_NAME is null then CR_FROM.NAME
	                         when CR_FROM.FUll_NAME = '' then CR_FROM.NAME
	                         else CR_FROM.FUll_NAME
	                       end,
	ISNULL(CR_FROM.ADDRESS, '') AS CONTRACTOR_FROM_ADDRESS,
	ISNULL(CR_FROM.PHONE, '') AS CONTRACTOR_FROM_PHONE,
	CONTRACTOR_TO_NAME = case
	                         when CR_TO.FUll_NAME is null then CR_TO.NAME
	                         when CR_TO.FUll_NAME = '' then CR_TO.NAME
	                         else CR_TO.FUll_NAME
	                       end,
	ISNULL(CR_TO.ADDRESS, '') AS CONTRACTOR_TO_ADDRESS,
	ISNULL(CR_TO.PHONE, '') AS CONTRACTOR_TO_PHONE,
	ISNULL(ST.NAME, '') AS STORE_FROM
FROM	DBO.ACT_RETURN_TO_CONTRACTOR AR
	JOIN DBO.CONTRACTOR CR_FROM ON AR.ID_CONTRACTOR_FROM = CR_FROM.ID_CONTRACTOR
	JOIN DBO.CONTRACTOR CR_TO ON AR.ID_CONTRACTOR_TO = CR_TO.ID_CONTRACTOR
	JOIN DBO.STORE ST ON ST.ID_STORE = AR.ID_STORE
WHERE	AR.ID_ACT_RETURN_TO_CONTRACTOR = @ID_DOCUMENT

SELECT
	I.INCOMING_NUMBER,
	I.INCOMING_DATE,
	I.INCOMING_BILL_NUMBER,
	I.INCOMING_BILL_DATE
FROM	DBO.INVOICE I
WHERE 	I.ID_INVOICE = @ID_DOCUMENT_BASE

SELECT
	G.NAME AS GOODS_NAME,
	U.NAME AS UNIT_NAME,
	A.QUANTITY AS QUANTITY,
	A.PRICE_VAT AS CONTRACTOR_PRICE_PER_UNIT
FROM
	ACT_RETURN_TO_CONTRACTOR_ITEM A
	INNER JOIN GOODS G ON G.ID_GOODS = A.ID_GOODS
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = A.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
WHERE A.ID_ACT_RETURN_TO_CONTRACTOR = @ID_DOCUMENT
ORDER BY GOODS_NAME

SELECT 
  COALESCE(CNT.NAME, '') AS COMPANY
FROM DBO.CONTRACTOR CNT
WHERE CNT.ID_CONTRACTOR = DBO.FN_CONST_CONTRACTOR_SELF()

SELECT
	DIR = DIRECTOR_FIO,
	BUH = BUH_FIO
FROM CONTRACTOR
WHERE ID_CONTRACTOR = (SELECT TOP 1 ID_CONTRACTOR FROM ENTERPRISE_BRANCH WHERE IS_SELF = 1)

RETURN 0
GO