SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_INV19') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_INV19 AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_INV19
	@XMLPARAM NTEXT AS
		
DECLARE	@HDOC INT
DECLARE @ID_GLOBAL UNIQUEIDENTIFIER
		
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
		
SELECT @ID_GLOBAL = ID_GLOBAL
FROM OPENXML(@HDOC , '/XML') WITH(ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL')
		
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
	CONTRACTOR = CASE WHEN ISNULL(C.FULL_NAME, '') = '' THEN C.NAME ELSE C.FULL_NAME END,
	STORE = S.NAME,
	DOC_NUM = I.DOC_NUM,
	DOC_DATE = CONVERT(VARCHAR(10), I.DOC_DATE, 104)
FROM INVENTORY_SVED I
	INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
	INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = S.ID_CONTRACTOR
WHERE I.ID_INVENTORY_GLOBAL = @ID_GLOBAL

SELECT
	GOODS_NAME = G.NAME,
	GOODS_CODE = G.CODE,
	OKEI_CODE = U.OKEI_CODE,
	UNIT_NAME = CAST(SR.NUMERATOR AS VARCHAR(5)) + '/' + CAST(SR.DENOMINATOR AS VARCHAR(5)) + ' ' + U.NAME,
	QUANTITY_REM = CASE WHEN IVI.ID_LOT_GLOBAL IS NOT NULL THEN ISNULL((SELECT SUM(LM.QUANTITY_ADD - LM.QUANTITY_SUB) FROM LOT_MOVEMENT LM WHERE LM.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL AND LM.DATE_OP < ISV.DOC_DATE), 0) ELSE 0 END,
	QUANTITY = IVI.QUANTITY,
	PRICE_SAL = IVI.PRICE_SAL	
FROM INVENTORY_SVED ISV
	INNER JOIN INVENTORY_VED IV ON IV.ID_INVENTORY_SVED_GLOBAL = ISV.ID_INVENTORY_GLOBAL
	INNER JOIN INVENTORY_VED_ITEM IVI ON IVI.ID_INVENTORY_VED_GLOBAL = IV.ID_INVENTORY_VED_GLOBAL
	INNER JOIN GOODS G ON G.ID_GOODS = IVI.ID_GOODS
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = IVI.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	LEFT JOIN LOT L ON L.ID_LOT_GLOBAL = IVI.ID_LOT_GLOBAL
WHERE ISV.ID_INVENTORY_GLOBAL = @ID_GLOBAL 
	AND CASE WHEN IVI.ID_LOT_GLOBAL IS NOT NULL THEN ISNULL((SELECT SUM(LM.QUANTITY_ADD - LM.QUANTITY_SUB) FROM LOT_MOVEMENT LM WHERE LM.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL AND LM.DATE_OP < ISV.DOC_DATE), 0) ELSE 0 END <> IVI.QUANTITY
ORDER BY G.NAME

RETURN 0
GO

--exec REPEX_INV19 N'<XML><ID_GLOBAL>95CB3959-4533-42CD-81AE-A42AFB53266A</ID_GLOBAL></XML>'
