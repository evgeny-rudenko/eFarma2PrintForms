SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_INVENTORY_VED_ALL') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_INVENTORY_VED_ALL AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_INVENTORY_VED_ALL
    @XMLPARAM NTEXT
AS

DECLARE	@HDOC INT, @ID_INVENTORY_GLOBAL UNIQUEIDENTIFIER, @ID_STORE BIGINT, @DOC_DATE DATETIME, @FULL BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT , @XMLPARAM OUTPUT
	SELECT TOP 1 @ID_INVENTORY_GLOBAL = ID_INVENTORY_GLOBAL FROM OPENXML(@HDOC , '/XML') WITH(
        ID_INVENTORY_GLOBAL UNIQUEIDENTIFIER 'ID_INVENTORY_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT 
	@ID_STORE = ID_STORE,
	@FULL = [FULL],
	@DOC_DATE = DOC_DATE
FROM INVENTORY_SVED I
WHERE ID_INVENTORY_GLOBAL = @ID_INVENTORY_GLOBAL

SELECT TOP 1
    DOC_DATE = I.DOC_DATE,
    DOC_NUM = I.DOC_NUM,
    DOC_NAME = 'Инвентаризационная ведомость № ' + RTRIM(I.DOC_NUM) + ' от ' + CONVERT(VARCHAR , I.DOC_DATE , 104),
    STORE_NAME = S.NAME,
    CONTRACTOR_NAME = C.NAME
FROM INVENTORY_SVED I
    INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
    INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = S.ID_CONTRACTOR
WHERE I.ID_INVENTORY_GLOBAL = @ID_INVENTORY_GLOBAL

DECLARE @TEMP_T TABLE
(
	GOODS_NAME VARCHAR(255),
	GOODS_CODE VARCHAR(40),
	SERIAL_NUMBER VARCHAR(2061),
	PRICE_SUP MONEY,
	PRICE_SAL MONEY,
	QUANTITY_DOC MONEY,
	QUANTITY_FACT MONEY,
	SCALING_RATIO_NAME VARCHAR(162),
	PVAT_SAL MONEY
)

INSERT INTO @TEMP_T
SELECT
	GOODS_NAME = GOODS_NAME,
	GOODS_CODE = GOODS_MNEMOCODE,
	SERIAL_NUMBER = SERIES,
	PRICE_SUP = PRICE_SUP,
	PRICE_SAL = PRICE_SAL,
	QUANTITY_DOC = QUANTITY_REM,
	QUANTITY_FACT = QUANTITY,
	SCALING_RATIO_NAME = SCALING_RATIO_NAME,
	PVAT_SAL = PVAT_SAL
FROM MV_INVENTORY_VED_ITEM IVI
	INNER JOIN INVENTORY_VED IV ON IV.ID_INVENTORY_VED_GLOBAL = IVI.ID_INVENTORY_VED_GLOBAL
	INNER JOIN INVENTORY_SVED I ON I.ID_INVENTORY_GLOBAL = IV.ID_INVENTORY_SVED_GLOBAL
WHERE I.ID_INVENTORY_GLOBAL = @ID_INVENTORY_GLOBAL

IF (@FULL = 1)
BEGIN
INSERT INTO @TEMP_T
SELECT
	GOODS_NAME = G.NAME,
	GOODS_CODE = G.MNEMOCODE,
	SERIAL_NUMBER = ISNULL(S.SERIES_NUMBER + ' / ', ' /') + ISNULL(CONVERT(VARCHAR(10), S.BEST_BEFORE,104), ''),
	PRICE_SUP = L.PRICE_SUP,
	PRICE_SAL = L.PRICE_SAL,
	QUANTITY_DOC = (SELECT SUM(LM.QUANTITY_ADD - LM.QUANTITY_SUB) FROM LOT_MOVEMENT LM WHERE LM.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL AND LM.DATE_OP < @DOC_DATE),
	QUANTITY_FACT = 0,
	SCALING_RATIO_NAME = CONVERT(VARCHAR, NUMERATOR) + '/' + CONVERT(VARCHAR, DENOMINATOR) + ' ' + U.NAME,
	PVAT_SAL = L.PVAT_SAL
FROM LOT L
	INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES
WHERE L.ID_STORE = @ID_STORE AND
	NOT EXISTS(
        SELECT TOP 1 1
        FROM INVENTORY_VED_ITEM IVI
            INNER JOIN INVENTORY_VED IV ON IV.ID_INVENTORY_VED_GLOBAL = IVI.ID_INVENTORY_VED_GLOBAL
            INNER JOIN INVENTORY_SVED I ON I.ID_INVENTORY_GLOBAL = IV.ID_INVENTORY_SVED_GLOBAL
        WHERE I.ID_INVENTORY_GLOBAL = @ID_INVENTORY_GLOBAL AND IVI.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
    ) AND (SELECT SUM(LM.QUANTITY_ADD - LM.QUANTITY_SUB) FROM LOT_MOVEMENT LM WHERE LM.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL AND LM.DATE_OP < @DOC_DATE) <> 0
END

SELECT * FROM @TEMP_T
ORDER BY GOODS_NAME

SELECT
	DIR = DIRECTOR_FIO,
	BUH = BUH_FIO
FROM CONTRACTOR
WHERE ID_CONTRACTOR = (SELECT TOP 1 ID_CONTRACTOR FROM ENTERPRISE_BRANCH WHERE IS_SELF = 1)

RETURN 0
GO

--exec REPEX_INVENTORY_VED_ALL N'<XML><ID_INVENTORY_GLOBAL>5717A84E-EF1A-43A0-BAD1-2E585F0D5B35</ID_INVENTORY_GLOBAL></XML>'
