IF (OBJECT_ID('BOOK_PURCHASES') IS NULL) EXEC ('CREATE PROCEDURE BOOK_PURCHASES AS RETURN')
GO
ALTER PROCEDURE BOOK_PURCHASES(
    @XMLPARAM NTEXT
)
AS

    DECLARE @HDOC INT
    
    DECLARE 
        @DATE_FROM DATETIME, 
        @DATE_TO DATETIME, 
        @SEL_STORE BIT, 
        @SEL_CONTRACTOR BIT
	DECLARE @CONTRACTORS TABLE(CONTRACTOR_NAME VARCHAR(128))

    EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM OUT
    SELECT 
        @DATE_FROM = DATE_FROM,
        @DATE_TO = DATE_TO

    FROM OPENXML(@HDOC, '/XML') WITH(
        DATE_FROM DATETIME 'DATE_FROM',
        DATE_TO DATETIME 'DATE_TO'
    )

-----покупатель
    SELECT
        ID_CONTRACTOR
    INTO #CONTRACTORS
    FROM OPENXML(@HDOC, '/XML/ID_CONTRACTOR') WITH(
        ID_CONTRACTOR BIGINT '.'
    )

    EXEC SP_XML_REMOVEDOCUMENT @HDOC

    EXEC USP_RANGE_DAYS @DATE_FROM OUT, @DATE_TO OUT
    EXEC USP_RANGE_NORM @DATE_FROM OUT, @DATE_TO OUT

--от кого
	SELECT
		S.ID_STORE,
		S.ID_CONTRACTOR
	INTO #STORES
	FROM #CONTRACTORS C
	LEFT JOIN STORE S ON C.ID_CONTRACTOR = S.ID_CONTRACTOR

IF (SELECT COUNT(*) FROM #STORES)!=0
BEGIN
	SELECT 
	    MNEMOCODE = I.MNEMOCODE,
	    I.ID_CONTRACTOR_SUPPLIER,
	    STATE = I.DOCUMENT_STATE,
	    DATE_AND_NUM = ISNULL(CONVERT(VARCHAR, I.INCOMING_BILL_DATE, 104),'')+ISNULL('/'+I.INCOMING_BILL_NUMBER,''),
	    BILL_NUMBER = I.INCOMING_BILL_NUMBER,
	    BILL_DATE = CONVERT(VARCHAR, I.INCOMING_BILL_DATE, 104),
	    DATE_PAYMENT = I.DATE_PAYMENT,
	    DOC_DATE = I.DOCUMENT_DATE,
	    ID_CONTRACTOR = C.ID_CONTRACTOR,
	    CONTRACTOR_NAME = C.NAME,
	    INN = C.INN,
	    KPP = C.KPP,
	    SUPP_SUM_VAT = SUM(II.SUPPLIER_SUM_VAT),
	    SUPPLIER_VAT_RATE_18 = SUM(CASE WHEN II.SUPPLIER_VAT = 18.0 THEN II.SUPPLIER_VAT_SUM ELSE 0 END),
	    SUPPLIER_SUM_18 = SUM(CASE WHEN II.SUPPLIER_VAT = 18.0 THEN II.SUPPLIER_SUM ELSE 0 END),
	    SUPPLIER_VAT_RATE_10 = SUM(CASE WHEN II.SUPPLIER_VAT = 10.0 THEN II.SUPPLIER_VAT_SUM ELSE 0 END),
	    SUPPLIER_SUM_10 = SUM(CASE WHEN II.SUPPLIER_VAT = 10.0 THEN II.SUPPLIER_SUM ELSE 0 END),
	    SUPPLIER_SUM_0 = SUM(CASE WHEN II.SUPPLIER_VAT = 0.0 THEN II.SUPPLIER_SUM ELSE 0 END)
	FROM INVOICE_ITEM II(NOLOCK)
    INNER JOIN INVOICE I ON I.ID_INVOICE_GLOBAL = II.ID_INVOICE_GLOBAL 
			AND I.DOCUMENT_STATE = 'PROC' 
			AND (I.ID_STORE IN (SELECT ID_STORE FROM #STORES)) AND (I.DOCUMENT_DATE BETWEEN @DATE_FROM AND @DATE_TO)
    INNER JOIN CONTRACTOR C(NOLOCK) ON C.ID_CONTRACTOR = I.ID_CONTRACTOR_SUPPLIER
	GROUP BY I.MNEMOCODE,I.INCOMING_BILL_NUMBER,I.INCOMING_BILL_DATE,I.DATE_PAYMENT,I.DOCUMENT_DATE,C.ID_CONTRACTOR,C.NAME,C.INN,C.KPP,I.DOCUMENT_STATE,I.ID_CONTRACTOR_SUPPLIER
	ORDER BY I.DOCUMENT_DATE

-- 	SELECT 
-- 	   BUYER_NAME = NAME,
-- 	   BUYER_INN_KPP = ISNULL(INN,'')+ISNULL(' / '+KPP,''),
-- 	   BUH_FIO = ISNULL(BUH_FIO,'')
-- 	FROM CONTRACTOR 
-- 	WHERE ID_CONTRACTOR IN (SELECT ID_CONTRACTOR FROM #STORES)

	INSERT INTO @CONTRACTORS
	SELECT 
		CONTRACTOR_NAME = [NAME]
	FROM CONTRACTOR 
	WHERE ID_CONTRACTOR IN (SELECT ID_CONTRACTOR FROM #CONTRACTORS)

    DECLARE @CONTRACTOR_STRING VARCHAR(4000)

    SELECT 
        @CONTRACTOR_STRING = ISNULL(@CONTRACTOR_STRING+' ,'+C.CONTRACTOR_NAME, C.CONTRACTOR_NAME)
    FROM @CONTRACTORS C
	SELECT CONTRACTOR_NAME = @CONTRACTOR_STRING

END

RETURN
GO
--exec BOOK_PURCHASES @xmlParam = N'<XML><DATE_FROM>2008-01-01T14:02:00.906</DATE_FROM><DATE_TO>2008-09-01T14:02:00.906</DATE_TO></XML>'
--exec BOOK_PURCHASES @xmlParam = N'<XML><DATE_FROM>2008-01-01T14:02:00.906</DATE_FROM><DATE_TO>2008-09-25T14:02:00.906</DATE_TO></XML>'
--exec BOOK_PURCHASES @xmlParam = N'<XML><DATE_FROM>2008-09-01T00:00:00.000</DATE_FROM><DATE_TO>2008-12-31T00:00:00.000</DATE_TO><ID_CONTRACTOR>5271</ID_CONTRACTOR><ID_CONTRACTOR>5274</ID_CONTRACTOR><ID_CONTRACTOR>5275</ID_CONTRACTOR></XML>'

