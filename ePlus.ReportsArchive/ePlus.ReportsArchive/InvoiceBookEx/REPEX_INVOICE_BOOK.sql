IF (OBJECT_ID('REPEX_INVOICE_BOOK') IS NULL) EXEC ('CREATE PROCEDURE REPEX_INVOICE_BOOK AS RETURN')
GO
ALTER PROCEDURE REPEX_INVOICE_BOOK(
    @XMLPARAM NTEXT
)
AS

    DECLARE @HDOC INT
    
    DECLARE @DATE_FROM DATETIME, @DATE_TO DATETIME, @DETAIL BIT
    EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM OUT
    SELECT 
        @DATE_FROM = DATE_FROM,
        @DATE_TO = DATE_TO,
        @DETAIL = DETAIL
    FROM OPENXML(@HDOC, '/XML') WITH(
        DATE_FROM DATETIME 'DATE_FROM',
        DATE_TO DATETIME 'DATE_TO',
        DETAIL BIT 'DETAIL'
    )

    SELECT
        ID_CONTRACTOR
    INTO #CONTRACTORS
    FROM OPENXML(@HDOC, '/XML/ID_CONTRACTOR') WITH(
        ID_CONTRACTOR BIGINT '.'
    )

    SELECT
        S.ID_STORE,
        S.ID_CONTRACTOR
    INTO #STORES
    FROM OPENXML(@HDOC, '/XML/ID_STORE') WITH(
        ID_STORE BIGINT '.'
    ) A
    INNER JOIN STORE S ON S.ID_STORE = A.ID_STORE

    EXEC SP_XML_REMOVEDOCUMENT @HDOC

    EXEC USP_RANGE_DAYS @DATE_FROM OUT, @DATE_TO OUT
    EXEC USP_RANGE_NORM @DATE_FROM OUT, @DATE_TO OUT

    INSERT INTO #STORES
    SELECT 
        ID_STORE,
        ID_CONTRACTOR
    FROM STORE S
    WHERE EXISTS (SELECT NULL FROM #CONTRACTORS C
                  WHERE S.ID_CONTRACTOR = C.ID_CONTRACTOR
                  AND NOT EXISTS (SELECT NULL FROM #STORES S1
                                  WHERE S.ID_CONTRACTOR = C.ID_CONTRACTOR))
 
    IF (NOT EXISTS (SELECT NULL FROM #STORES))
        INSERT INTO #STORES
        SELECT
            ID_STORE,
            ID_CONTRACTOR
        FROM STORE

    SELECT
        ID_INVOICE_ITEM = II.ID_INVOICE_ITEM,
        ID_CONTRACTOR_SUPPLIER = I.ID_CONTRACTOR_SUPPLIER,
        CONTRACTOR_NAME = C.NAME,
        MNEMOCODE = I.MNEMOCODE,
        DATE = I.DOCUMENT_DATE,
        INCOMING_NUMBER = I.INCOMING_NUMBER,
        INCOMING_DATE = I.INCOMING_DATE,
        CONTRACTOR_SUM_VAT = II.SUPPLIER_SUM_VAT,
        RETAIL_SUM = II.RETAIL_SUM_VAT,
        SUPPLIER_VAT_RATE_18 = CASE WHEN II.SUPPLIER_VAT = 18.0 THEN II.SUPPLIER_VAT_SUM ELSE 0 END,
        SUPPLIER_VAT_RATE_10 = CASE WHEN II.SUPPLIER_VAT = 10.0 THEN II.SUPPLIER_VAT_SUM ELSE 0 END,
        RETAIL_VAT_RATE_18 = CASE WHEN II.RETAIL_VAT = 18.0 THEN II.RETAIL_VAT_SUM ELSE 0 END,
        RETAIL_VAT_RATE_10 = CASE WHEN II.RETAIL_VAT = 10.0 THEN II.RETAIL_VAT_SUM ELSE 0 END,
        SUM_NP = 0,
        SUM_PROD = 0
    INTO #INVOICE_ITEMS
    FROM INVOICE_ITEM II(NOLOCK)
    INNER JOIN INVOICE I ON I.ID_INVOICE_GLOBAL = II.ID_INVOICE_GLOBAL
    INNER JOIN CONTRACTOR C(NOLOCK) ON C.ID_CONTRACTOR = I.ID_CONTRACTOR_SUPPLIER
    WHERE I.DOCUMENT_STATE = 'PROC' 
    AND I.ID_STORE IN (SELECT ID_STORE FROM #STORES)
    AND I.DOCUMENT_DATE BETWEEN @DATE_FROM AND @DATE_TO
    
    IF (@DETAIL=1) BEGIN
        SELECT 
            ID_CONTRACTOR_SUPPLIER, 
            CONTRACTOR_NAME,
        	MNEMOCODE, 
            DATE,
            INCOMING_NUMBER, 
            INCOMING_DATE,
        	CONTRACTOR_SUM_VAT = SUM(CONTRACTOR_SUM_VAT), 
            RETAIL_SUM = SUM(RETAIL_SUM),
        	SUPPLIER_VAT_RATE_18 = SUM(SUPPLIER_VAT_RATE_18), 
            SUPPLIER_VAT_RATE_10 = SUM(SUPPLIER_VAT_RATE_10),
        	RETAIL_VAT_RATE_18 = SUM(RETAIL_VAT_RATE_18), 
            RETAIL_VAT_RATE_10 = SUM(RETAIL_VAT_RATE_10),
        	SUM_NP = 0, SUM_PROD = 0
        FROM #INVOICE_ITEMS I
        GROUP BY I.ID_CONTRACTOR_SUPPLIER, I.CONTRACTOR_NAME,I.MNEMOCODE, I.DATE, I.INCOMING_NUMBER, I.INCOMING_DATE
    END
    ELSE BEGIN
        SELECT
            ID_CONTRACTOR_SUPPLIER, 
            CONTRACTOR_NAME,
        	CONTRACTOR_SUM_VAT = SUM(CONTRACTOR_SUM_VAT), 
            RETAIL_SUM = SUM(RETAIL_SUM),
        	SUPPLIER_VAT_RATE_18 = SUM(SUPPLIER_VAT_RATE_18), 
            SUPPLIER_VAT_RATE_10 = SUM(SUPPLIER_VAT_RATE_10),
        	RETAIL_VAT_RATE_18 = SUM(RETAIL_VAT_RATE_18), 
            RETAIL_VAT_RATE_10 = SUM(RETAIL_VAT_RATE_10),
        	SUM_NP = 0 , SUM_PROD = 0
        FROM #INVOICE_ITEMS I
        GROUP BY ID_CONTRACTOR_SUPPLIER, CONTRACTOR_NAME
    END
    
    SELECT COMPANY = ISNULL(NAME , '')
    FROM CONTRACTOR
    WHERE ID_CONTRACTOR = DBO.FN_CONST_CONTRACTOR_SELF()
RETURN
GO



