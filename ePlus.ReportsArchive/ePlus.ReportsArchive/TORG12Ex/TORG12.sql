SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF (OBJECT_ID('DBO.FN_CERT_LIST_4_REPEX_TORG12') IS NULL)	EXEC ('CREATE FUNCTION DBO.FN_CERT_LIST_4_REPEX_TORG12() RETURNS VARCHAR(4000) AS BEGIN RETURN CONVERT(VARCHAR(4000), NULL) END')
GO
ALTER FUNCTION DBO.FN_CERT_LIST_4_REPEX_TORG12(
    @ID_SERIES BIGINT
)
RETURNS VARCHAR(4000)
AS
BEGIN
    DECLARE @RESULT VARCHAR(4000)
    SELECT 
        @RESULT = ISNULL(@RESULT + '; ' + CONVERT(NVARCHAR(12), C.CERT_DATE, 104) + ' выдан ' + C.ISSUED_BY, ISNULL(CONVERT(NVARCHAR(12), C.CERT_DATE, 104) + ' выдан ' + C.ISSUED_BY, ''))
    FROM CERTIFICATE C
    WHERE ID_SERIES = @ID_SERIES
    RETURN @RESULT
END
GO

IF OBJECT_ID('DBO.REPEX_TORG12') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_TORG12 AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_TORG12(
    @XMLPARAM NTEXT
) AS

DECLARE @HDOC INT, @ID_GLOBAL UNIQUEIDENTIFIER

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
    SELECT TOP 1 @ID_GLOBAL = ID_GLOBAL
    FROM OPENXML(@HDOC, '/XML') WITH(
        ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL'
    )
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
    GOODS = G.NAME + ' [' + P.NAME + ', ' + COALESCE(C.NAME, '') + ']',
    GOODS_FEDERAL_CODE = G.FEDERAL_CODE,
    UNIT_NAME = U.NAME + '(' + CAST(SR.NUMERATOR AS VARCHAR) + '/' + CAST(SR.DENOMINATOR AS VARCHAR) + ')',
    OKEI_CODE = U.OKEI_CODE,    
    QUANTITY = II.QUANTITY,
    CONTRACTOR_PRICE_PER_UNIT = II.SUPPLIER_PRICE_VAT,
    SUM_CONTRACTOR_PRICE = II.SUPPLIER_SUM,
    VAT_RATE = II.SUPPLIER_VAT,
    SUM_VAT = II.SUPPLIER_VAT_SUM,
    SUM_CONTRACTOR_PRICE_VAT = II.SUPPLIER_SUM_VAT,
	CERTIFICATION_INFO = DBO.FN_CERT_LIST_4_REPEX_TORG12(S.ID_SERIES),
    S.BEST_BEFORE,
    S.SERIES_NUMBER
FROM INVOICE I
    INNER JOIN INVOICE_ITEM II ON II.ID_INVOICE = I.ID_INVOICE
    INNER JOIN GOODS G ON G.ID_GOODS = II.ID_GOODS
    INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
    LEFT JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
    INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = II.ID_SCALING_RATIO
    INNER JOIN UNIT U ON SR.ID_UNIT = U.ID_UNIT
    LEFT JOIN SERIES S ON S.ID_SERIES = II.ID_SERIES
WHERE I.ID_INVOICE_GLOBAL = @ID_GLOBAL
ORDER BY GOODS

SELECT TOP 1
    INVOICE_NUMBER = I.MNEMOCODE,
    INVICE_DATE = I.DOCUMENT_DATE,
    SUPPLIER_NAME = COALESCE(NULLIF(RTRIM(LTRIM(SUP.FULL_NAME)),''), SUP.[NAME], ''),
    SUPPLIER_OKPO = SUP.OKPO,
    SUPPLIER_DESCRIPT =
        CASE WHEN ISNULL(SUP.FULL_NAME, '') != '' THEN SUP.FULL_NAME ELSE SUP.[NAME] END +
        COALESCE(' ИНН:' + SUP.INN, '') +
        COALESCE(' Адрес:' + SUP.ADDRESS, '') +
        COALESCE(' Телефон:' + SUP.PHONE, '') +
        COALESCE(' КПП:' + SUP.KPP, '') +
        COALESCE(' Банк:' + SUP.BANK + ' ' + SUP.BANK_ADDRESS , ' Банк:' + SUP.BANK, ' Банк:' + SUP.BANK_ADDRESS, '') +
        COALESCE(' р/c:' + SUP.ACCOUNT, '') +
        COALESCE(' к/с:' + SUP.CORR_ACCOUNT, '') +
        COALESCE(' БИК:' + SUP.BIK, ''),
    CUSTOMER_NAME = COALESCE(NULLIF(RTRIM(LTRIM(CONT.FULL_NAME)),''), CONT.[NAME], ''),
    CUSTOMER_OKPO = CONT.OKPO,
    CUSTOMER_DESCRIPT = 
        CASE WHEN ISNULL(CONT.FULL_NAME, '') != '' THEN CONT.FULL_NAME ELSE CONT.NAME END +
        COALESCE(' ИНН:' + CONT.INN, '') +
        COALESCE(' Адрес:' + CONT.ADDRESS, '') +
        COALESCE(' Телефон:' + CONT.PHONE, '') +
        COALESCE(' КПП:' + CONT.KPP, '') +
        COALESCE(' Банк:' + CONT.BANK + ' ' + CONT.BANK_ADDRESS, ' Банк:' + CONT.BANK, ' Банк:' + CONT.BANK_ADDRESS, '') +
        COALESCE(' р/c:' + CONT.ACCOUNT, '') +
        COALESCE(' к/с:' + CONT.CORR_ACCOUNT, '') +
        COALESCE(' БИК:' + CONT.BIK, '') , 
    PAYER_NAME = NULL,
    STORE_NAME = S.NAME,
    INCOMING_NUMBER =
        COALESCE(I.INCOMING_NUMBER, '') + COALESCE(' от ' + CONVERT(VARCHAR, I.INCOMING_DATE, 4), '')
FROM INVOICE I
    INNER JOIN CONTRACTOR SUP ON SUP.ID_CONTRACTOR = I.ID_CONTRACTOR_SUPPLIER
    INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE 
    INNER JOIN CONTRACTOR CONT ON CONT.ID_CONTRACTOR = S.ID_CONTRACTOR
WHERE I.ID_INVOICE_GLOBAL = @ID_GLOBAL

RETURN 0
GO

--exec REPEX_TORG12 '<XML><ID_GLOBAL>a8370009-c8ed-46f4-9373-51ae6e0ea372</ID_GLOBAL></XML>'