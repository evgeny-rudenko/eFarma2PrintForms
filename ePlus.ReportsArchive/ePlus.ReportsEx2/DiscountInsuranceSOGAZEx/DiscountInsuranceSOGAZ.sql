SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF (OBJECT_ID('DBO.REPEX_DISCOUNT_INSURANCE_SOGAZ') IS NULL) EXEC ('CREATE PROCEDURE DBO.REPEX_DISCOUNT_INSURANCE_SOGAZ AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_DISCOUNT_INSURANCE_SOGAZ
    @XMLPARAM NTEXT
AS
    DECLARE @DATE_FROM DATETIME,
            @DATE_TO DATETIME,
            @ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL UNIQUEIDENTIFIER,
            @SORT_ORDER VARCHAR(4000),
            @ALL_LGOT BIT,
            @ALL_POLICY BIT,
            @ALL_MEMBER BIT,
            @ALL_GOODS BIT,
            @ALL_VALUE BIT
    DECLARE @HDOC INT
    EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM
    SELECT 
        @DATE_FROM = DATE_FROM,
        @DATE_TO = DATE_TO,
        @ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL = ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL,
        @SORT_ORDER = SORT_ORDER
    FROM OPENXML(@HDOC, '/XML', 2) WITH(
        DATE_FROM DATETIME,
        DATE_TO DATETIME,
        ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL UNIQUEIDENTIFIER,
        SORT_ORDER VARCHAR(4000)
    )
    
    SELECT
        *
    INTO #LGOT
    FROM OPENXML(@HDOC, '/XML/LGOTS/LGOT', 2) WITH(
        ID_DISCOUNT2_CAT_LGOT_GLOBAL UNIQUEIDENTIFIER
    )
    IF (@@ROWCOUNT=0)
        SET @ALL_LGOT =1
    SELECT
        VALUE = DISCOUNT_VALUE
    INTO #DISCOUNT_VALUE
    FROM OPENXML(@HDOC, '/XML/PRECENTS/PRECENT', 2) WITH(
        DISCOUNT_VALUE MONEY
    )
    IF (@@ROWCOUNT=0)
        SET @ALL_VALUE =1
    SELECT
        *
    INTO #POLICY
    FROM OPENXML(@HDOC, '/XML/POLICIES/POLICY', 2) WITH(
        ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL UNIQUEIDENTIFIER
    )
    IF (@@ROWCOUNT=0)
        SET @ALL_POLICY =1
    SELECT
        *
    INTO #MEMBER
    FROM OPENXML(@HDOC, '/XML/MEMBERS/MEMBER', 2) WITH(
        ID_DISCOUNT2_MEMBER_GLOBAL UNIQUEIDENTIFIER
    )
    IF (@@ROWCOUNT=0)
        SET @ALL_MEMBER =1
    
    SELECT
        *
    INTO #GOODS
    FROM OPENXML(@HDOC, '/XML/GOODSLIST/GOODS', 2) WITH(
        ID_GOODS BIGINT
    )
    IF (@@ROWCOUNT=0)
        SET @ALL_GOODS =1
    
    EXEC SP_XML_REMOVEDOCUMENT @HDOC
    EXEC USP_RANGE_DAYS @DATE_FROM OUT, @DATE_TO OUT
    EXEC USP_RANGE_NORM @DATE_FROM OUT, @DATE_TO OUT
    
    SELECT 
        MEMBER = DM.LASTNAME + ISNULL(' '+DM.FIRSTNAME+'.','')+ISNULL(' '+DM.MIDDLENAME+'.', ''),
        MEMBER_BIRTHDAY = DM.BIRTHDAY,
        POLICY = ISNULL(DIP.PREFIX+'/','')+DIP.NUMBER,
        POLICY_NUMBER = DIP.NUMBER,
        POLICY_PREFIX = DIP.PREFIX,
        CAT_LGOT = DCL.NAME,
        DATE = C.DATE_CHEQUE,
        GOODS_NAME = G.NAME,
        PRICE = CI.PRICE,
        QTY = CI.QUANTITY,
        SUMM_WO_DISCOUNT = CI.SUMM+CI.SUMM_DISCOUNT,
        SUMM_WITH_DISCOUNT = CI.SUMM,
        DISCOUNT_PRECENT = ROUND(CI.SUMM_DISCOUNT*100/(CI.SUMM+CI.SUMM_DISCOUNT), 0),
        STORE_NAME = S.NAME,
        CONTRACTOR_NAME = CONTRACTOR.NAME
    INTO #RESULT
    FROM DISCOUNT2_MAKE_ITEM DMI
    INNER JOIN CHEQUE_ITEM CI ON CI.ID_CHEQUE_ITEM_GLOBAL = DMI.ID_CHEQUE_ITEM_GLOBAL
    INNER JOIN CHEQUE C ON C.ID_CHEQUE_GLOBAL = CI.ID_CHEQUE_GLOBAL
    INNER JOIN DISCOUNT2_INSURANCE_POLICY DIP ON DIP.ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL = DMI.ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL
    INNER JOIN DISCOUNT2_MEMBER DM ON DM.ID_DISCOUNT2_MEMBER_GLOBAL = DIP.ID_DISCOUNT2_MEMBER_GLOBAL
    INNER JOIN DISCOUNT2_CAT_LGOT DCL ON DCL.ID_DISCOUNT2_CAT_LGOT_GLOBAL = DIP.ID_DISCOUNT2_CAT_LGOT_GLOBAL
    INNER JOIN GOODS G ON G.ID_GOODS = CI.ID_GOODS
    INNER JOIN LOT_MOVEMENT LM ON LM.ID_DOCUMENT_ITEM = CI.ID_CHEQUE_ITEM_GLOBAL
    INNER JOIN LOT L ON L.ID_LOT_GLOBAL = LM.ID_LOT_GLOBAL
    INNER JOIN STORE S ON S.ID_STORE = L.ID_STORE
    INNER JOIN CONTRACTOR ON CONTRACTOR.ID_CONTRACTOR = S.ID_CONTRACTOR
    WHERE DMI.ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL IS NOT NULL
    AND C.DATE_CHEQUE BETWEEN @DATE_FROM AND @DATE_TO
    AND @ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL = DIP.ID_DISCOUNT2_INSURANCE_COMPANY_GLOBAL
    AND (@ALL_LGOT = 1 OR EXISTS (SELECT NULL FROM #LGOT WHERE #LGOT.ID_DISCOUNT2_CAT_LGOT_GLOBAL = DIP.ID_DISCOUNT2_CAT_LGOT_GLOBAL))
    AND (@ALL_MEMBER = 1 OR EXISTS (SELECT NULL FROM #MEMBER WHERE #MEMBER.ID_DISCOUNT2_MEMBER_GLOBAL = DIP.ID_DISCOUNT2_MEMBER_GLOBAL))
    AND (@ALL_POLICY = 1 OR EXISTS (SELECT NULL FROM #POLICY WHERE #POLICY.ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL = DIP.ID_DISCOUNT2_INSURANCE_POLICY_GLOBAL))
    AND (@ALL_GOODS = 1 OR EXISTS (SELECT NULL FROM #GOODS WHERE #GOODS.ID_GOODS = CI.ID_GOODS))
    AND (@ALL_VALUE = 1 OR EXISTS (SELECT NULL FROM #DISCOUNT_VALUE 
         WHERE #DISCOUNT_VALUE.VALUE =ROUND(CI.SUMM_DISCOUNT*100/(CI.SUMM+CI.SUMM_DISCOUNT), 0)))
     AND C.ID_CHEQUE_GLOBAL NOT IN (SELECT ID_DOCUMENT_BASE_GLOBAL FROM CHEQUE WHERE ID_DOCUMENT_BASE_GLOBAL IS NOT NULL AND CHEQUE_TYPE = 'RETURN')
     AND C.CHEQUE_TYPE <> 'RETURN'
     AND CI.ID_CHEQUE_ITEM NOT IN (SELECT ID_BASE_DOCUMENT_ITEM
          FROM ACT_RETURN_TO_BUYER_ITEM ABI 
          INNER JOIN ACT_RETURN_TO_BUYER AB ON ABI.ID_ACT_RETURN_TO_BUYER_GLOBAL = AB.ID_ACT_RETURN_TO_BUYER_GLOBAL
          WHERE AB.DOCUMENT_STATE = 'PROC')
--    ORDER BY 7, 1

	DECLARE @QUERY NVARCHAR(4000)
	SET @QUERY ='
	SELECT 
        MEMBER,
        MEMBER_BIRTHDAY,
        POLICY,
        POLICY_NUMBER,
        POLICY_PREFIX,
        CAT_LGOT,
        DATE,
        GOODS_NAME,
        PRICE,
        QTY,
        SUMM_WO_DISCOUNT,
        SUMM_WITH_DISCOUNT,
        DISCOUNT_PRECENT,
        STORE_NAME,
        CONTRACTOR_NAME
	FROM #RESULT'
	IF (LEN(@SORT_ORDER)>0)
		SET @QUERY = @QUERY +' ORDER BY '+@SORT_ORDER
	EXEC SP_EXECUTESQL @QUERY
RETURN
GO
