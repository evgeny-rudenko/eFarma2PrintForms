SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF (OBJECT_ID('REPEX_INVOICE_OUT_BILL') IS NULL) EXEC ('CREATE PROCEDURE REPEX_INVOICE_OUT_BILL AS RETURN')
GO
ALTER PROCEDURE REPEX_INVOICE_OUT_BILL
    @XMLPARAM NTEXT 
AS

DECLARE	@HDOC INT  
DECLARE @ID_GLOBAL UNIQUEIDENTIFIER

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
	SELECT	TOP 1 @ID_GLOBAL = ID_GLOBAL 
	FROM OPENXML(@HDOC, '/XML') 
	WITH(ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
	BILL_NUMBER = 'СЧ' + RIGHT(I.MNEMOCODE, 9),
	BILL_DATE   = CONVERT(VARCHAR , I.[DATE], 104),
	SUPPLIER_INFO = ISNULL('ИНН ' + S.INN + ', ', '') + ISNULL('КПП ' + S.KPP + ', ', '') + ISNULL(S.[FULL_NAME] + ' ', ISNULL(S.NAME + ' ', '')) + ISNULL(S.BANK + ', ', '') + ISNULL(S.ADDRESS + ', ', '') + ISNULL('тел.: ' + S.PHONE, ''),
	RECEPIENT_INFO = ISNULL('ИНН ' + R.INN + ', ', '') + ISNULL('КПП ' + R.KPP + ', ', '') + ISNULL(R.[FULL_NAME]+' ', ISNULL(R.NAME + ' ', '')) + ISNULL(R.BANK + ', ', '') + ISNULL(R.ADDRESS + ', ', '') + ISNULL('тел.: ' + R.PHONE, ''),
	COMPANY_NAME = CASE WHEN (S.FULL_NAME IS NULL OR S.FULL_NAME = '') THEN S.NAME ELSE S.FULL_NAME END,
	COMPANY_ADDRESS = S.ADDRESS,
	COMPANY_PHONE = S.PHONE,
	COMPANY_INN = S.INN,
	COMPANY_KPP = S.KPP,
	COMPANY_BANK = S.BANK,
	COMPANY_BANK_ADDRESS = S.BANK_ADDRESS,
	COMPANY_ACCOUNT = S.ACCOUNT,
	COMPANY_CORR_BANK = S.CORR_BANK,
	COMPANY_BIK = S.BIK,
	COMPANY_CORR_ACCOUNT = S.CORR_ACCOUNT,
	DIRECTOR_FIO = S.DIRECTOR_FIO,
	BUH_FIO = S.BUH_FIO
FROM INVOICE_OUT I
	INNER JOIN STORE ST ON I.ID_STORE = ST.ID_STORE
	INNER JOIN CONTRACTOR S ON ST.ID_CONTRACTOR = S.ID_CONTRACTOR
	INNER JOIN CONTRACTOR R ON I.ID_CONTRACTOR_TO = R.ID_CONTRACTOR
WHERE I.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL

SELECT
	GOODS_NAME = G.[NAME] + ', производитель: ' + P.[NAME] + '(' + C.NAME + '), НДС ' + CONVERT(VARCHAR(4), L.VAT_SAL) + '%',
	QUANTITY = II.QUANTITY,
	UNIT_NAME = U.SHORT_NAME,
	RETAIL_PRICEV = II.PRICE_SAL, 
	RETAIL_SUMV   = II.PRICE_SAL * (CASE WHEN II.QUANTITY <> 0 THEN II.QUANTITY ELSE 1 END),
	VAT = II.PVAT_SAL * II.QUANTITY,
    VAT_SUM10 = CASE WHEN L.VAT_SAL = 10 THEN II.PVAT_SAL * II.QUANTITY ELSE 0 END,
    VAT_SUM18 = CASE WHEN L.VAT_SAL = 18 THEN II.PVAT_SAL * II.QUANTITY ELSE 0 END
FROM INVOICE_OUT_ITEM II
	INNER JOIN LOT L ON II.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
	INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	INNER JOIN COUNTRY C ON P.ID_COUNTRY = C.ID_COUNTRY
WHERE II.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL
ORDER BY GOODS_NAME

RETURN
GO

--exec REPEX_INVOICE_OUT_BILL '<XML><ID_GLOBAL>ed181a5a-18b3-404b-81f8-9ecb2037a165</ID_GLOBAL></XML>'