SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('REPEX_INVOICE_OL') IS NULL EXEC('CREATE PROCEDURE REPEX_INVOICE_OL AS RETURN')
GO

ALTER PROCEDURE REPEX_INVOICE_OL
    @XMLPARAM NTEXT
AS

/* PARAMETERS */
DECLARE @HDOC INT
DECLARE @ID_INVOICE BIGINT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
	SELECT TOP 1 @ID_INVOICE = ID_INVOICE
	FROM OPENXML(@HDOC, '/XML') 
	WITH(ID_INVOICE BIGINT 'ID_INVOICE')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

select
	MNEMOCODE		= i.MNEMOCODE,
	INVOICE_DATE	= i.DOCUMENT_DATE,
	INCONING_NUMBER	= i.INCOMING_NUMBER,
	INCOMING_DATE	= i.INCOMING_DATE,
	PAY_BEFORE		= i.DATE_PAYMENT,
	AU_NAME			= IsNull(c.FULL_NAME, c.NAME),
	SUP_NAME		= IsNull(sup.FULL_NAME, sup.NAME),
	STORE			= s.NAME,
	MOL				= IsNull(DBO.FN_FORMAT_FULL_NAME_SHORT(E.LAST_NAME, E.FIRST_NAME, E.MIDDLE_NAME), '________________')
from INVOICE	i
join STORE		s	on s.ID_STORE = i.ID_STORE
join CONTRACTOR	c	on c.ID_CONTRACTOR = s.ID_CONTRACTOR
join CONTRACTOR	sup	on sup.ID_CONTRACTOR = i.ID_CONTRACTOR_SUPPLIER
left
join EMPLOYEE	e	on e.ID_EMPLOYEE = c.ID_MOL
where
	i.ID_INVOICE = @ID_INVOICE

select
	GOOD_NAME	= g.NAME,
	SERIA		= s.SERIES_NUMBER,
	QUANTITY	= ii.QUANTITY,
	UNIT		= u.SHORT_NAME,
	K			= (select max(sr_i.DENOMINATOR) from SCALING_RATIO sr_i where sr_i.ID_GOODS = ii.ID_GOODS),
	PRICE_SUP	= ii.SUPPLIER_PRICE_VAT,
	SUM_SUP		= ii.SUPPLIER_SUM_VAT,
	TAX_RATE	= ii.SUPPLIER_VAT,
	VAT_SUM_SUP	= ii.SUPPLIER_VAT_SUM,
	ADPRICE_SAL	= ii.RETAIL_ADPRICE,
	PRICE_SAL	= ii.RETAIL_PRICE_VAT,
	SUM_SAL		= ii.RETAIL_SUM_VAT
into #ITEMS
from INVOICE		i
join INVOICE_ITEM	ii	on ii.ID_INVOICE_GLOBAL = i.ID_INVOICE_GLOBAL
join GOODS			g	on g.ID_GOODS = ii.ID_GOODS
join SERIES			s	on s.ID_SERIES = ii.ID_SERIES
join SCALING_RATIO	sr	on sr.ID_SCALING_RATIO = ii.ID_SCALING_RATIO
join UNIT			u	on sr.ID_UNIT = u.ID_UNIT
where
	i.ID_INVOICE = @ID_INVOICE
order by
	ii.ID_INVOICE_ITEM

select *
from #ITEMS

select
	TOTAL_SUM_SUP		= sum(SUM_SUP),
	TOTAL_SUM_VAT10		= sum(case when TAX_RATE = 10.0 then VAT_SUM_SUP else 0 end),
	TOTAL_SUM_VAT18		= sum(case when TAX_RATE = 18.0 then VAT_SUM_SUP else 0 end),
	TOTAL_SUM_SAL		= sum(SUM_SAL),
	TOTAL_SUM_ADPRICE	= sum(SUM_SAL) - sum(SUM_SUP)
from #ITEMS

RETURN 0
GO
/*
exec REPEX_INVOICE_OL N'<XML><ID_INVOICE>2889</ID_INVOICE></XML>'
*/
