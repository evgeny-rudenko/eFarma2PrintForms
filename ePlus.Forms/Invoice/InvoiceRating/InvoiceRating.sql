SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_INVOICE_RATING') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_INVOICE_RATING AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_INVOICE_RATING
    @XMLPARAM NTEXT AS

DECLARE @HDOC INT
DECLARE @ID_GLOBAL UNIQUEIDENTIFIER

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
SELECT @ID_GLOBAL = ID_GLOBAL FROM OPENXML(@HDOC, '/XML') WITH(ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT 
	SELF_NAME = CASE WHEN ISNULL(C.FULL_NAME, '') = '' THEN C.NAME ELSE C.FULL_NAME END,	
	DIR = C.DIRECTOR_FIO
FROM REPLICATION_CONFIG AS RC 
	INNER JOIN CONTRACTOR AS C ON RC.ID_CONTRACTOR_GLOBAL = C.ID_CONTRACTOR_GLOBAL
WHERE RC.IS_SELF = 1

SELECT
	DOC_NUM = I.MNEMOCODE,
	TO_NAME = CASE WHEN ISNULL(C_TO.FULL_NAME, '') = '' THEN C_TO.NAME ELSE C_TO.FULL_NAME END,
	FR_NAME = CASE WHEN ISNULL(C_FR.FULL_NAME, '') = '' THEN C_FR.NAME ELSE C_FR.FULL_NAME END,
	I_NUM = CASE WHEN I.INCOMING_NUMBER IS NULL THEN NULL
						WHEN I.INCOMING_DATE IS NULL THEN NULL
						ELSE I.INCOMING_NUMBER + ' от ' + CONVERT(VARCHAR, I.INCOMING_DATE, 104) END,
	I_DATE = I.INCOMING_DATE
FROM INVOICE I
	INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
	INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = S.ID_CONTRACTOR
	INNER JOIN CONTRACTOR C_FR ON C_FR.ID_CONTRACTOR = I.ID_CONTRACTOR_SUPPLIER
WHERE I.ID_INVOICE_GLOBAL = @ID_GLOBAL

SELECT
	GOODS_NAME = G.NAME,
	PRODUCER_NAME = P.NAME,
	COUNTRY_NAME = CN.NAME,
	SERIES = S.SERIES_NUMBER,
	BEST_BEFORE = S.BEST_BEFORE,
	QUANTITY = II.QUANTITY,
	REGISTER_PRICE = G.REGISTER_PRICE,
	PRICE_PROD = II.PRODUCER_PRICE,
	PRICE_SUP = II.SUPPLIER_PRICE,
	VAT_SUP_RATE = II.SUPPLIER_VAT,
	VAT_SUP_SUM = II.SUPPLIER_VAT_SUM,
	SUP_ADPRICE = II.SUPPLIER_ADPRICE,
	SUP_ADPRICE_SUM = (II.SUPPLIER_PRICE - II.PRODUCER_PRICE) * II.QUANTITY,
	VPRICE_SUP = II.SUPPLIER_PRICE_VAT,
	VSUM_SUP = II.SUPPLIER_PRICE_VAT * II.QUANTITY,
	SAL_ADPRICE = II.RETAIL_ADPRICE,
	SAL_ADPRICE_SUM = II.RETAIL_ADPRICE_PER_UNIT * II.QUANTITY,
	PRICE_SAL = II.RETAIL_PRICE,
	VAT_SAL_RATE = II.RETAIL_VAT,	
	VAT_SAL_SUM = II.RETAIL_VAT_SUM,
	VPRICE_SAL = II.RETAIL_PRICE_VAT,
	VPRICE_SUM = II.RETAIL_SUM_VAT,
	BOX_NUM = II.BOX
FROM INVOICE_ITEM II
	INNER JOIN GOODS G ON G.ID_GOODS = II.ID_GOODS
	INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
	INNER JOIN COUNTRY CN ON CN.ID_COUNTRY = P.ID_COUNTRY		
	LEFT JOIN SERIES S ON S.ID_SERIES = II.ID_SERIES
WHERE II.ID_INVOICE_GLOBAL = @ID_GLOBAL
ORDER BY G.NAME

RETURN 0
GO

--exec REPEX_INVOICE_RATING N'<XML><ID_GLOBAL>65527C09-F747-45E7-8CD8-674E8E4E1637</ID_GLOBAL></XML>'