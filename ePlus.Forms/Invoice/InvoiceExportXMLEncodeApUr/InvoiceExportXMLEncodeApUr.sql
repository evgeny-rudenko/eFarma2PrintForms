IF (OBJECT_ID('USP_EXPORT_INVOICE_2_XML_AP_UR') IS NULL) EXEC ('CREATE PROCEDURE USP_EXPORT_INVOICE_2_XML_AP_UR AS RETURN')
GO
ALTER PROCEDURE USP_EXPORT_INVOICE_2_XML_AP_UR
    @ID_GLOBAL UNIQUEIDENTIFIER 
AS
    DECLARE @DOC TABLE(
        FID BIGINT NOT NULL PRIMARY KEY IDENTITY,
        DOC_NAME VARCHAR(300),
        ID_SUPPLIER BIGINT
    )
    
    DECLARE @HEADER TABLE(
        SID BIGINT NOT NULL PRIMARY KEY IDENTITY,
        FID BIGINT NOT NULL,

        SUPPLIER_NAME VARCHAR(300),
        SVAT_SUPPLIER MONEY,
        SUM_SUPPLIER MONEY,
        SUM_SUPPLIER_WITH_PVAT MONEY,
        SVAT_RETAIL MONEY,
        SUM_RETAIL MONEY,
        INCOMING_NUMBER VARCHAR(300),
        INCOMING_DATE VARCHAR(300),
        INCOMING_BILL_NUMBER VARCHAR(300),
        INCOMING_BILL_DATE DATETIME,
        COMMENT VARCHAR(300),
        CONTRACTOR_TO_NAME VARCHAR(300),
        COUNT_POSITION INT,
        SUPPLIER_ADDRESS VARCHAR(300),
        SUPPLIER_INN VARCHAR(300),
        SUPPLIER_PHONE VARCHAR(300),
        SUPPLIER_ACCOUNT VARCHAR(300),
        SUPPLIER_CITY VARCHAR(300),
        SUPPLIER_BANK VARCHAR(300),
        SUPPLIER_BANK_OFFICE VARCHAR(300),
        SUPPLIER_BIK VARCHAR(300),
        SUPPLIER_CORR_ACCOUNT VARCHAR(300),
        SUPPLIER_EMAIL VARCHAR(300),
        SUPPLIER_KPP VARCHAR(300),
        SUPPLIER_OKONH VARCHAR(300),
        SUPPLIER_OKPO VARCHAR(300)
    )

    DECLARE @ITEM TABLE(
        TID BIGINT NOT NULL PRIMARY KEY IDENTITY,
        SID BIGINT NOT NULL,
        FID BIGINT NOT NULL,

        NUMERATOR INT, 
        DENOMINATOR INT,
        UNIT_NAME VARCHAR(300),
        
        GOODS_CODE VARCHAR(300),
        GOODS VARCHAR(300),
        PRODUCER VARCHAR(300),
        COUNTRY VARCHAR(300), 
        IMPORTANT BIT,
        REGISTER_PRICE MONEY,
        REGISTRATION_DATE DATETIME,
        
        QUANTITY MONEY,
        PRODUCER_PRICE MONEY,
        
        SUPPLIER_VAT_PER_UNIT MONEY,
        SUPPLIER_ADPRICE MONEY,
        SUPPLIER_PRICE MONEY,
        SUPPLIER_VAT MONEY,
        SUPPLIER_PRICE_VAT MONEY,
        SUPPLIER_SUM MONEY,
        SUPPLIER_VAT_SUM MONEY,
        SUPPLIER_SUM_VAT MONEY,
        
        RETAIL_ADPRICE MONEY,
        RETAIL_PRICE MONEY,
        RETAIL_VAT MONEY,
        RETAIL_PRICE_VAT MONEY,
        RETAIL_SUM MONEY,
        RETAIL_VAT_SUM MONEY,
        RETAIL_SUM_VAT MONEY,
        
        SERIES_NUMBER VARCHAR(300),
        BEST_BEFORE DATETIME,
        GTD_NUMBER VARCHAR(300),
        BAR_CODE VARCHAR(300),

        ID_SERIES BIGINT,
        ID_GOODS_KIND BIGINT,
        GOODS_KIND_MNEMOCODE VARCHAR(300),
        GOODS_KIND_NAME VARCHAR(300),
        GOODS_KIND_COMMENT VARCHAR(300),
        SUPPLIER_NAME VARCHAR(300),
        INCOMING_NUM VARCHAR(300),
        INCOMING_DATE DATETIME,
		EAN13  VARCHAR(100)
    )

    DECLARE @CERT TABLE(
        TID BIGINT NOT NULL,
        SID BIGINT NOT NULL,
        FID BIGINT NOT NULL,

        CERT_NUMBER VARCHAR(300),
        ISSUED_BY VARCHAR(300),
        CERT_DATE DATETIME, 
        CERT_END_DATE DATETIME,
        SERIES_NUMBER VARCHAR(300),
        BEST_BEFORE DATETIME
    )


    INSERT INTO @DOC(
        DOC_NAME,
        ID_SUPPLIER
    )
    SELECT DISTINCT 
        I.MNEMOCODE+'_'+dbo.FN_DATE_2_VARCHAR(I.DOCUMENT_DATE),
        C.ID_CONTRACTOR 
        --select  *
    FROM INVOICE I
    INNER JOIN LOT_MOVEMENT LM ON LM.ID_DOCUMENT = I.ID_INVOICE_GLOBAL
    INNER JOIN LOT L ON LM.ID_LOT_GLOBAL = L.ID_LOT_GLOBAL
    INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = L.ID_SUPPLIER 
    WHERE I.ID_INVOICE_GLOBAL = @ID_GLOBAL

    INSERT INTO @HEADER(
        FID,
        SUPPLIER_NAME,
        SVAT_SUPPLIER,
        SUM_SUPPLIER,
        SUM_SUPPLIER_WITH_PVAT,
        SVAT_RETAIL,
        SUM_RETAIL,
        INCOMING_NUMBER,
        INCOMING_DATE,
        INCOMING_BILL_NUMBER,
        INCOMING_BILL_DATE,
        COMMENT,
        CONTRACTOR_TO_NAME,
        COUNT_POSITION,
        
        SUPPLIER_ADDRESS,
        SUPPLIER_INN,
        SUPPLIER_PHONE,
        SUPPLIER_ACCOUNT,
        SUPPLIER_CITY,
        SUPPLIER_BANK,
        SUPPLIER_BANK_OFFICE,
        SUPPLIER_BIK,
        SUPPLIER_CORR_ACCOUNT,
        SUPPLIER_EMAIL,
        SUPPLIER_KPP,
        SUPPLIER_OKONH,
        SUPPLIER_OKPO
    )
    SELECT
        D.FID,
        C.NAME,

        SVAT_SUPPLIER = SUM(L.PVAT_SUP*II.QUANTITY),
        SUM_SUPPLIER = SUM((L.PRICE_SUP-L.PVAT_SUP)*II.QUANTITY),
        SUM_SUPPLIER_WITH_PVAT = SUM((L.PRICE_SUP)*II.QUANTITY),
        SVAT_RETAIL = SUM(L.PVAT_SAL*II.QUANTITY),
        SUM_RETAIL = SUM(L.PRICE_SAL*II.QUANTITY),
        I.MNEMOCODE,
        INCOMING_DATE = CONVERT(VARCHAR(10), I.DOCUMENT_DATE, 104),
        NULL,
        NULL,
        I.COMMENT,
        C_TO.NAME,
        COUNT_POSITION = COUNT(*),
        -----------------
        SUPPLIER_ADDRESS = C.ADDRESS,
        SUPPLIER_INN = C.INN,
        SUPPLIER_PHONE = C.PHONE,
        SUPPLIER_ACCOUNT = C.ACCOUNT,
        SUPPLIER_CITY = '',
        SUPPLIER_BANK = C.BANK,
        SUPPLIER_BANK_OFFICE = C.BANK_ADDRESS,
        SUPPLIER_BIK = C.BIK,
        SUPPLIER_CORR_ACCOUNT = C.CORR_ACCOUNT,
        SUPPLIER_EMAIL = C.EMAIL,
        
        SUPPLIER_KPP = C.KPP,
        SUPPLIER_OKONH = C.OKONH,
        SUPPLIER_OKPO = C.OKPO
        --select * from contractor
    FROM INVOICE I 
    INNER JOIN INVOICE_ITEM II ON II.ID_INVOICE_GLOBAL = I.ID_INVOICE_GLOBAL
    INNER JOIN LOT_MOVEMENT LM ON LM.ID_DOCUMENT = II.ID_INVOICE_GLOBAL AND LM.ID_DOCUMENT_ITEM = II.ID_INVOICE_ITEM_GLOBAL
    INNER JOIN LOT L ON L.ID_LOT_GLOBAL = LM.ID_LOT_GLOBAL
    INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = L.ID_SUPPLIER
    INNER JOIN STORE S_TO ON S_TO.ID_STORE = I.ID_STORE
    INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = S_TO.ID_CONTRACTOR
    INNER JOIN @DOC D ON D.ID_SUPPLIER = L.ID_SUPPLIER
    WHERE I.ID_INVOICE_GLOBAL = @ID_GLOBAL
    GROUP BY D.FID,C.NAME,I.MNEMOCODE,I.DOCUMENT_DATE,I.COMMENT,C_TO.NAME,
        C.ADDRESS,
        C.INN,
        C.PHONE,
        C.ACCOUNT,
        C.BANK,
        C.BANK_ADDRESS,
        C.BIK,
        C.CORR_ACCOUNT,
        C.EMAIL,
        C.KPP,
        C.OKONH,
        C.OKPO

    INSERT INTO @ITEM(
        SID,
        FID,

        NUMERATOR, 
        DENOMINATOR,
        UNIT_NAME,
        
        GOODS_CODE,
        GOODS,
        PRODUCER,
        COUNTRY, 
        IMPORTANT,
        REGISTER_PRICE,
        REGISTRATION_DATE,
        
        QUANTITY,
        PRODUCER_PRICE,
        
        SUPPLIER_VAT_PER_UNIT,
        SUPPLIER_ADPRICE,
        SUPPLIER_PRICE,
        SUPPLIER_VAT,
        SUPPLIER_PRICE_VAT,
        SUPPLIER_SUM,
        SUPPLIER_VAT_SUM,
        SUPPLIER_SUM_VAT,
        
        RETAIL_ADPRICE,
        RETAIL_PRICE,
        RETAIL_VAT,
        RETAIL_PRICE_VAT,
        RETAIL_SUM,
        RETAIL_VAT_SUM,
        RETAIL_SUM_VAT,
        
        SERIES_NUMBER,
        BEST_BEFORE,
        GTD_NUMBER,
        BAR_CODE,

        ID_SERIES,
        
        ID_GOODS_KIND,
        GOODS_KIND_MNEMOCODE,
        GOODS_KIND_NAME,
        GOODS_KIND_COMMENT,
        SUPPLIER_NAME,
        INCOMING_NUM,
        INCOMING_DATE,
        EAN13
    )
    
    SELECT
        H.SID,
        H.FID,

        NUMERATOR = SR.NUMERATOR, 
        DENOMINATOR = SR.DENOMINATOR,
        UNIT_NAME = U.SHORT_NAME,
        
        GOODS_CODE = (SELECT TOP 1 CODE FROM GOODS_CODE WHERE ID_CONTRACTOR = L.ID_SUPPLIER AND ID_GOODS = G.ID_GOODS),
        GOODS = G.NAME,
        PRODUCER = P.NAME,
        COUNTRY = C.NAME, 
        IMPORTANT = CONVERT(BIT, G.IMPORTANT),
        REGISTER_PRICE = G.REGISTER_PRICE,
        REGISTRATION_DATE = G.REGISTRATION_DATE,
        
        QUANTITY = II.QUANTITY,
        PRODUCER_PRICE = L.PRICE_PROD,
        
        SUPPLIER_VAT_PER_UNIT = L.PVAT_SUP,
        SUPPLIER_ADPRICE = II.SUPPLIER_ADPRICE,-- CASE ISNULL(L.PRICE_PROD,0) WHEN 0 THEN NULL ELSE ((L.PRICE_SUP * 100)/L.PRICE_PROD)-100 END,
        SUPPLIER_PRICE = L.PRICE_SUP - L.PVAT_SUP,
        SUPPLIER_VAT = L.VAT_SUP,
        SUPPLIER_PRICE_VAT = L.PRICE_SUP,
        SUPPLIER_SUM = (L.PRICE_SUP * II.QUANTITY) - (L.PVAT_SUP * II.QUANTITY),
        SUPPLIER_VAT_SUM = L.PVAT_SUP * II.QUANTITY,
        SUPPLIER_SUM_VAT = L.PRICE_SUP * II.QUANTITY,
        
        RETAIL_ADPRICE = II.RETAIL_ADPRICE,--CASE ISNULL(L.PRICE_SUP,0) WHEN 0 THEN NULL ELSE ((L.PRICE_SAL * 100)/L.PRICE_SUP)-100 END,
        RETAIL_PRICE = L.PRICE_SAL - L.PVAT_SAL,
        RETAIL_VAT = VAT_SAL,
        RETAIL_PRICE_VAT = L.PRICE_SAL,
        RETAIL_SUM = (L.PRICE_SAL * II.QUANTITY) - (L.PVAT_SAL * II.QUANTITY),
        RETAIL_VAT_SUM = L.PVAT_SAL * II.QUANTITY,
        RETAIL_SUM_VAT = L.PRICE_SAL * II.QUANTITY,
        
        SERIES_NUMBER = S.SERIES_NUMBER,
        BEST_BEFORE = S.BEST_BEFORE,
        GTD_NUMBER = L.GTD_NUMBER,
        BAR_CODE = L.INTERNAL_BARCODE,

        ID_SERIES = S.ID_SERIES,
        -----------------------
        ID_GOODS_KIND = GK.ID_GOODS_KIND,
        GOODS_KIND_MNEMOCODE = GK.MNEMOCODE,
        GOODS_KIND_NAME = GK.NAME,
        GOODS_KIND_COMMENT = GK.COMMENT
        -- select *
        --,s.ID_SERIES
        ,SUPPLIER_NAME = H.SUPPLIER_NAME
        ,INCOMING_NUM = L.INCOMING_NUM
        ,INCOMING_DATE = L.INCOMING_DATE
        ,EAN13 = (SELECT TOP 1 CODE FROM BAR_CODE AS BC WHERE BC.ID_GOODS = G.ID_GOODS)
        --,SUPPLIER_ADPRICE = II.SUPPLIER_ADPRICE
       -- ,RETAIL_ADPRICE = II.RETAIL_ADPRICE
     --,
     --select * from BAR_CODE
    FROM INVOICE_ITEM II --where II.ID_INVOICE_GLOBAL = 'B1F243BB-B575-4450-9A18-732253AFCF37'
    INNER JOIN LOT_MOVEMENT LM ON LM.ID_DOCUMENT = II.ID_INVOICE_GLOBAL 
                              AND LM.ID_DOCUMENT_ITEM = II.ID_INVOICE_ITEM_GLOBAL
                            -- AND LM.QUANTITY_SUB>0 -- 1409 (экспорт накладной)
    INNER JOIN LOT L ON L.ID_LOT_GLOBAL = LM.ID_LOT_GLOBAL
    INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
    INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
    INNER JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
    INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
    INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
    LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES
    INNER JOIN @DOC D ON D.ID_SUPPLIER = L.ID_SUPPLIER
    INNER JOIN @HEADER H ON D.FID = H.FID
    LEFT JOIN GOODS_KIND GK ON GK.ID_GOODS_KIND = G.ID_GOODS_KIND
   WHERE II.ID_INVOICE_GLOBAL = @ID_GLOBAL

    
    INSERT INTO @CERT(
        TID,
        SID,
        FID,

        CERT_NUMBER,
        ISSUED_BY,
        CERT_DATE,
        CERT_END_DATE,
        ------
        SERIES_NUMBER,
        BEST_BEFORE
    )
    SELECT
        I.TID,
        I.SID,
        I.FID,

        CERT_NUMBER,
        ISSUED_BY,
        CERT_DATE,
        CERT_END_DATE,
        
        SERIES_NUMBER = I.SERIES_NUMBER,
        BEST_BEFORE = I.BEST_BEFORE
    FROM @ITEM I 
    INNER JOIN CERTIFICATE C ON C.ID_SERIES = I.ID_SERIES


    SELECT 
        FID,
        DOC_NAME
    FROM @DOC

    SELECT 
        FID,
        SID,
        SUPPLIER_NAME,
        CONTRACTOR_TO_NAME,
        COUNT_POSITION,
        SVAT_SUPPLIER,
        SUM_SUPPLIER,
        SUM_SUPPLIER_WITH_PVAT,
        SVAT_RETAIL,
        SUM_RETAIL,
        INCOMING_NUMBER,
        INCOMING_DATE,
        INCOMING_BILL_NUMBER,
        INCOMING_BILL_DATE,
        COMMENT,
        ---------------
        SUPPLIER_ADDRESS,
        SUPPLIER_INN,
        SUPPLIER_PHONE,
        SUPPLIER_ACCOUNT,
        SUPPLIER_CITY,
        SUPPLIER_BANK,
        SUPPLIER_BANK_OFFICE,
        SUPPLIER_BIK,
        SUPPLIER_CORR_ACCOUNT,
        SUPPLIER_EMAIL,
        SUPPLIER_KPP,
        SUPPLIER_OKONH,
        SUPPLIER_OKPO
    FROM @HEADER        

    SELECT
        FID,
        SID,
        TID,

        NUMERATOR, 
        DENOMINATOR,
        UNIT_NAME,
        
        GOODS_CODE,
        GOODS,
        PRODUCER,
        COUNTRY, 
        IMPORTANT,
        REGISTER_PRICE,
        REGISTRATION_DATE,
        
        QUANTITY,
        PRODUCER_PRICE,
        
        SUPPLIER_VAT_PER_UNIT,
        SUPPLIER_ADPRICE,
        SUPPLIER_PRICE,
        SUPPLIER_VAT,
        SUPPLIER_PRICE_VAT,
        SUPPLIER_SUM,
        SUPPLIER_VAT_SUM,
        SUPPLIER_SUM_VAT,
        
        RETAIL_ADPRICE,
        RETAIL_PRICE,
        RETAIL_VAT,
        RETAIL_PRICE_VAT,
        RETAIL_SUM,
        RETAIL_VAT_SUM,
        RETAIL_SUM_VAT,
        
        SERIES_NUMBER,
        BEST_BEFORE,
        GTD_NUMBER,
        BAR_CODE,
        ---------------
        ID_GOODS_KIND,
        GOODS_KIND_MNEMOCODE,
        GOODS_KIND_NAME,
        GOODS_KIND_COMMENT,
        SUPPLIER_NAME,
        INCOMING_NUM,
        INCOMING_DATE,
        EAN13
    FROM @ITEM

    SELECT
        FID,
        SID,
        TID,
        CERT_NUMBER,
        ISSUED_BY,
        CERT_DATE, 
        CERT_END_DATE,
        ------------
        SERIES_NUMBER,
        BEST_BEFORE
    FROM @CERT

RETURN
GO

--EXEC USP_EXPORT_INVOICE_2_XML_AP_UR '6B7B82D9-8A62-4773-9F09-512C084B8166'


SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO 

IF OBJECT_ID('DBO.REMOVE_REPORT_BY_TYPE_NAME') IS NULL EXEC('CREATE PROCEDURE DBO.REMOVE_REPORT_BY_TYPE_NAME AS RETURN')
GO
ALTER PROCEDURE DBO.REMOVE_REPORT_BY_TYPE_NAME 
	@REPORT_TYPE_NAME VARCHAR(200) AS
	
DECLARE @id_meta_report BIGINT

	select 
		@id_meta_report = id_meta_report
	from meta_report
	where type_name = @REPORT_TYPE_NAME
	--select @id_meta_report
		
	DECLARE @SQL NVARCHAR(200)
	SET @SQL = N'delete from META_REPORT_2_REPORT_GROUPS
				where id_meta_report = @id_meta_report'
	IF (OBJECT_ID('META_REPORT_2_REPORT_GROUPS') IS NOT NULL)
		EXEC SP_EXECUTESQL @SQL, N'@id_meta_report BIGINT', @id_meta_report=@id_meta_report
		

	SET @SQL = N'delete from meta_report_settings_csv_export
		where id_meta_report = @id_meta_report'
	IF (OBJECT_ID('meta_report_settings_csv_export') IS NOT NULL)
		EXEC SP_EXECUTESQL @SQL, N'@id_meta_report BIGINT', @id_meta_report=@id_meta_report
		

	SET @SQL = N'delete from meta_report_settings_visible
		where id_meta_report = @id_meta_report'
	IF (OBJECT_ID('meta_report_settings_visible') IS NOT NULL)
		EXEC SP_EXECUTESQL @SQL, N'@id_meta_report BIGINT', @id_meta_report=@id_meta_report
		

	SET @SQL = N'delete from meta_report_settings_managed
				where id_meta_report = @id_meta_report'
	IF (OBJECT_ID('meta_report_settings_managed') IS NOT NULL)
		EXEC SP_EXECUTESQL @SQL, N'@id_meta_report BIGINT', @id_meta_report=@id_meta_report


	SET @SQL = N'delete from meta_report_settings_archive
				where id_meta_report = @id_meta_report'
	IF (OBJECT_ID('meta_report_settings_archive') IS NOT NULL)
		EXEC SP_EXECUTESQL @SQL, N'@id_meta_report BIGINT', @id_meta_report=@id_meta_report


	delete from meta_report
	where id_meta_report = @id_meta_report

RETURN 0
GO

EXEC DBO.REMOVE_REPORT_BY_TYPE_NAME 'InvoiceExportXMLEncodeApUr.InvoiceOutExportXML'

