SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('REPEX_ACT_RETURN_TO_CONTRACTOR') IS NULL EXEC('CREATE PROCEDURE REPEX_ACT_RETURN_TO_CONTRACTOR AS RETURN')
GO
ALTER PROCEDURE REPEX_ACT_RETURN_TO_CONTRACTOR
	@XMLPARAM NTEXT AS
		
DECLARE	@HDOC INT ,	@ID_ACT_RETURN_TO_CONTRACTOR BIGINT , @ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL UNIQUEIDENTIFIER
		
EXEC	SP_XML_PREPAREDOCUMENT @HDOC OUTPUT , @XMLPARAM OUTPUT
		
	SELECT	TOP 1 @ID_ACT_RETURN_TO_CONTRACTOR = ID_ACT_RETURN_TO_CONTRACTOR , @ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL = ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL
	FROM	OPENXML(@HDOC , '/XML') WITH(ID_ACT_RETURN_TO_CONTRACTOR BIGINT 'ID_ACT_RETURN_TO_CONTRACTOR' , ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL UNIQUEIDENTIFIER 'ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL')
		
EXEC	SP_XML_REMOVEDOCUMENT @HDOC

SELECT	TOP 1 CO_ID = CO.ID_CONTRACTOR ,
	CO_NAME = CASE WHEN CO.FULL_NAME IS NULL OR CO.FULL_NAME = '' THEN CO.NAME ELSE CO.FULL_NAME END ,
	CO_FULLNAME = CO.FULL_NAME ,
	CO_INN = CO.INN ,
	CO_ADDRESS = CO.ADDRESS ,
	CO_PHONE = CO.PHONE ,
	CO_BANK = CO.BANK ,
	CO_BANKADDRESS = CO.BANK_ADDRESS ,
	CO_ACCOUNT = CO.ACCOUNT ,
	CO_CORRACCOUNT = CO.CORR_ACCOUNT ,
	CO_BIK = CO.BIK
FROM	CONTRACTOR CO(NOLOCK)
WHERE	CO.ID_CONTRACTOR = (SELECT TOP 1 AC.ID_CONTRACTOR_FROM FROM ACT_RETURN_TO_CONTRACTOR AC(NOLOCK) WHERE ID_ACT_RETURN_TO_CONTRACTOR = @ID_ACT_RETURN_TO_CONTRACTOR OR ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL = @ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL)

-- выбираем данные для шапки документа
SELECT
	AC_ID = AC.ID_ACT_RETURN_TO_CONTRACTOR,
	AC_NUMBER = AC.MNEMOCODE,
	AC_DATE = CONVERT(VARCHAR , AC.DATE , 104),
	AC_STORE = (SELECT TOP 1 S.NAME FROM dbo.STORE S(NOLOCK) WHERE S.ID_STORE = AC.ID_STORE),
	AC_COMPANY = (SELECT TOP 1 CASE WHEN C.FULL_NAME IS NULL OR C.FULL_NAME = '' THEN C.NAME ELSE C.FULL_NAME END FROM dbo.CONTRACTOR C(NOLOCK) WHERE C.ID_CONTRACTOR = AC.ID_CONTRACTOR_TO)
into #TMP
FROM dbo.ACT_RETURN_TO_CONTRACTOR AC(NOLOCK)
WHERE AC.ID_ACT_RETURN_TO_CONTRACTOR = @ID_ACT_RETURN_TO_CONTRACTOR
	OR AC.ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL = @ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL

-- выбираем номер-дату документов-оснований
-- временная таблица со всеми номерами документов по партиям
select
	l.ID_DOCUMENT
into #actDocs
from dbo.LOT l
where l.ID_LOT_GLOBAL in (select aci.ID_LOT_GLOBAL from dbo.ACT_RETURN_TO_CONTRACTOR_ITEM aci where aci.ID_ACT_RETURN_TO_CONTRACTOR in (select t.AC_ID from #tmp t))

SELECT 
	I_NUMBER = a.I_NUMBER + CASE WHEN a.I_DATE = '' THEN '' ELSE ' от ' + a.I_DATE END
INTO #tmpDocs
FROM (

-- счет-фактура
select distinct
	I_NUMBER = COALESCE(i.INCOMING_NUMBER, i.MNEMOCODE, ''),		
	I_DATE = COALESCE(CONVERT(VARCHAR, i.INCOMING_DATE, 104), CONVERT(VARCHAR, i.DOCUMENT_DATE, 104), '')
from dbo.INVOICE i
where i.ID_INVOICE_GLOBAL in (select t.ID_DOCUMENT from #actDocs t)

union all 

-- перемещение
select distinct
	I_NUMBER = ISNULL(m.MNEMOCODE, ''),
	I_DATE = ISNULL(CONVERT(VARCHAR , m.[DATE] , 104), '')
from dbo.MOVEMENT m
where m.ID_MOVEMENT_GLOBAL in (select t.ID_DOCUMENT from #actDocs t)

union all

-- акт переоценки
select distinct
	I_NUMBER = ISNULL(ar.MNEMOCODE, ''),
	I_DATE = ISNULL(CONVERT(VARCHAR, ar.[DATE], 104), '')
from dbo.ACT_REVALUATION2 ar
where ar.ID_ACT_REVALUATION2_GLOBAL in (select t.ID_DOCUMENT from #actDocs t)

) a

declare @DocsNum varchar(4000)
set @DocsNum = ''

-- собираем строку с номерами документов
if exists (select * from #tmpDocs)
begin
	select
		@DocsNum = @DocsNum + ', ' + t.I_NUMBER
	from #tmpDocs t
	where ltrim(rtrim(t.I_NUMBER)) <> ''

	set @DocsNum = ltrim(rtrim(@DocsNum))
	set @DocsNum = right(@DocsNum, len(@DocsNum) - 2)
end

	SELECT TOP 1
		t.AC_ID,
		t.AC_NUMBER,
		t.AC_DATE,
		t.AC_STORE,
		t.AC_COMPANY,
		I_NUMBER = @DocsNum
	FROM #TMP t
	
SELECT	G_ID = G.ID_GOODS ,
	G_MODELCODE = G.MNEMOCODE ,
	G_RUSNAME = G.NAME ,
	G_SERIALNUMBER = (SELECT TOP 1 SN.SERIES_NUMBER FROM SERIES SN(NOLOCK) WHERE SN.ID_SERIES = ISNULL(L.ID_SERIES , 0)) ,
	G_BESTBEFORE = (SELECT TOP 1 CONVERT(VARCHAR , SN.BEST_BEFORE , 104) FROM SERIES SN(NOLOCK) WHERE SN.ID_SERIES = ISNULL(L.ID_SERIES , 0)) ,
	G_NAMESCALING = dbo.FN_SCALE_NAME_SHORT(ACI.ID_SCALING_RATIO),
	ACI_QUANTITY = ACI.QUANTITY ,

	ACI_PRICESUPP = ACI.PRICE_VAT,
	ACI_PVATSUPP = ACI.PRICE_VAT - ACI.PRICE,
	ACI_SUMSUPP = 0,
	ACI_PRICESAL = L.PRICE_SAL,
	ACI_PVATSAL = L.PVAT_SAL,
	ACI_SUMSAL = 0,
	ACI_SUMDISCOUNT = 0 ,
	ACI_TAXSALE = 0 
FROM GOODS G(NOLOCK) , LOT L(NOLOCK) , ACT_RETURN_TO_CONTRACTOR_ITEM ACI(NOLOCK)
WHERE G.ID_GOODS = ACI.ID_GOODS AND L.ID_LOT_GLOBAL = ACI.ID_LOT_GLOBAL AND (ACI.ID_ACT_RETURN_TO_CONTRACTOR = @ID_ACT_RETURN_TO_CONTRACTOR OR ACI.ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL = @ID_ACT_RETURN_TO_CONTRACTOR_GLOBAL)
ORDER BY G_RUSNAME
		
RETURN 0
GO
/*
exec REPEX_ACT_RETURN_TO_CONTRACTOR N'<XML>
<ID_ACT_RETURN_TO_CONTRACTOR>236</ID_ACT_RETURN_TO_CONTRACTOR>
</XML>'
*/