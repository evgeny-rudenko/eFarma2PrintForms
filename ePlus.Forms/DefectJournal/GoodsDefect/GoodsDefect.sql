SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF OBJECT_ID('DBO.REPEX_GOODS_DEFECT') IS NULL EXEC('CREATE PROCEDURE DBO.REPEX_GOODS_DEFECT AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_GOODS_DEFECT
    @XMLPARAM NTEXT AS

DECLARE @HDOC INT 
DECLARE @ID BIGINT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT, @XMLPARAM OUTPUT
SELECT @ID = ID FROM OPENXML(@HDOC, '/XML') WITH(ID BIGINT 'ID')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
	DOC_NAME = rtrim(DJ.MNEMOCODE) + ' от ' + CONVERT(VARCHAR(10), DJ.DOC_DATE, 104),
	STORE_NAME = ST.NAME,
	CONTRACTOR_NAME = CT.NAME
FROM DEFECT_JOURNAL DJ
	LEFT JOIN STORE ST ON ST.ID_STORE = DJ.ID_STORE
	LEFT JOIN CONTRACTOR CT ON CT.ID_CONTRACTOR = ST.ID_CONTRACTOR
WHERE DJ.ID_DEFECT_JOURNAL = @ID

SELECT
    GOODS_NAME = G.NAME,
    PRODUCER_NAME = P.NAME,
    SUPPLIER_NAME = SUP.NAME,
    QUANTITY_REM = ISNULL((SELECT SUM(QUANTITY_ADD - QUANTITY_SUB - QUANTITY_RES) FROM LOT_MOVEMENT WHERE ID_LOT_GLOBAL = L.ID_LOT_GLOBAL AND DATE_OP < DJ.DOC_DATE), 0),
    SERIES_NUMBER = S.SERIES_NUMBER,
    GOODS_NAME_X = DJD.GD_DRUG_TXT,
    PRODUCER_NAME_X = DJD.GD_MNF_NM, 
    SERIES_NUMBER_X = DJD.GD_SERIES_NUMBER,
    ARGUMENT = GD.ARGUMENT,
    LETTER = CASE WHEN GD.LETTER_DATE IS NULL THEN GD.LETTER_NR ELSE ISNULL(GD.LETTER_NR, '') + ' от ' + CONVERT(VARCHAR, GD.LETTER_DATE, 104) END
FROM DEFECT_JOURNAL_DETAIL DJD
	INNER JOIN DEFECT_JOURNAL DJ ON DJ.ID_DEFECT_JOURNAL = DJD.ID_DEFECT_JOURNAL
	INNER JOIN LOT L ON DJD.ID_LOT = L.ID_LOT
	INNER JOIN CONTRACTOR SUP ON SUP.ID_CONTRACTOR = L.ID_SUPPLIER
    INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES	
	LEFT JOIN PRODUCER P ON G.ID_PRODUCER = P.ID_PRODUCER
	LEFT JOIN GOODS_DEFECT GD ON GD.ID_GOODS_DEFECT_GLOBAL = DJD.ID_GOODS_DEFECT_GLOBAL
WHERE DJD.ID_DEFECT_JOURNAL = @ID
ORDER BY GOODS_NAME

RETURN 0
GO

--exec DBO.REPEX_GOODS_DEFECT N'<XML><ID>57</ID></XML>'