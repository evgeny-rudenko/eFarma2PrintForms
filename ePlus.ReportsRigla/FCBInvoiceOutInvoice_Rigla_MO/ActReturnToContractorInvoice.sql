SET NOCOUNT ON
SET QUOTED_IDENTIFIER OFF
GO

IF (OBJECT_ID('DBO.REPEX_INVOICE_OUT_INVOICE_RIGLA') IS NULL) EXEC ('CREATE PROCEDURE DBO.REPEX_INVOICE_OUT_INVOICE_RIGLA AS RETURN')
GO
ALTER PROCEDURE DBO.REPEX_INVOICE_OUT_INVOICE_RIGLA
	@XMLPARAM NTEXT AS
		
DECLARE	@HDOC INT
DECLARE	@ID_GLOBAL UNIQUEIDENTIFIER
		
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUTPUT , @XMLPARAM OUTPUT
SELECT @ID_GLOBAL = ID_GLOBAL FROM OPENXML(@HDOC , '/XML') WITH(ID_GLOBAL UNIQUEIDENTIFIER 'ID_GLOBAL')
EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
	[INVOICE_OUT_NAME] = I.MNEMOCODE,
	[INVOICE_OUT_DATE] = I.DATE,
	[CONTRACTOR_FROM_NAME] = C_FR.LEGAL_PERS + ' (' + C_FR.LEGAL_PERS_SHORT + ')',
	[CONTRACTOR_FROM_ADDRESS] = C_FR.UR_ADDRESS,
	[CONTRACTOR_FROM_INN] = ISNULL(C_FR.INN, 'Не указано') + '/' + ISNULL(C_FR.KPP, 'Не указано'),

--	[GCONTRACTOR] = ISNULL(ISNULL(C_FR.LEGAL_PERS + ' ', C_FR.LEGAL_PERS_SHORT + ' '), '') + ISNULL(C_FR.NAME, 'Не указано') + ', ' + ISNULL(C_FR.ADDRESS, 'Адрес не указан'), 
	[GCONTRACTOR] = ISNULL(C_FR.FULL_NAME, '') + CASE WHEN ISNULL(C_FR.ADDRESS, '') = '' THEN '' ELSE ', ' + C_FR.ADDRESS END,
	[GCONTRACTOR_TO_NAME] = ISNULL(C_TO.FULL_NAME, C_TO.NAME) + ', ' + ISNULL(C_TO.ADDRESS, 'Адрес не указан'),
	[CONTRACTOR_TO_NAME] = CASE WHEN ISNULL(C_P.FULL_NAME, '') = '' THEN C_P.NAME ELSE C_P.FULL_NAME END,
	[CONTRACTOR_TO_ADDRESS] = C_P.UR_ADDRESS,
	[CONTRACTOR_TO_INN] = ISNULL(C_P.INN, 'Не указано') + '/' + ISNULL(C_P.KPP, 'Не указано')
FROM INVOICE_OUT I
	INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
	INNER JOIN CONTRACTOR C_FR ON C_FR.ID_CONTRACTOR = S.ID_CONTRACTOR
	INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = I.ID_CONTRACTOR_TO
	INNER JOIN CONTRACTOR C_P ON C_P.ID_CONTRACTOR = I.ID_PAYER
WHERE I.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL

DECLARE @IS_SUP BIT
DECLARE @IS_USE_VAT BIGINT
SELECT 
	@IS_SUP = IS_SUPPLIER,
	@IS_USE_VAT = C_TO.USE_VAT
FROM INVOICE_OUT I
	INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = I.ID_CONTRACTOR_TO
WHERE I.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL

--select @is_sup, @is_use_vat

IF (@IS_SUP = 1)
BEGIN
	IF (@IS_USE_VAT = 1)
	BEGIN

	SELECT
		[GOODS_NAME] = G.NAME,
		[UNIT_NAME] = U.SHORT_NAME,
		[QUANTITY] = II.QUANTITY,
		[PRICE_SAL] = L.PRICE_SUP - L.PVAT_SUP,
		[SUM_SAL_WITHOUT_VAT] = II.QUANTITY * (L.PRICE_SUP - L.PVAT_SUP),
		[VAT_SAL] = CASE WHEN L.PVAT_SUP = 0 THEN CAST('Без НДС' AS VARCHAR(10)) ELSE CAST(L.VAT_SUP AS VARCHAR(9)) + '%' END,
		[PSUM_SAL] = II.QUANTITY * L.PVAT_SUP,
		[SUM_SAL] = II.QUANTITY * L.PRICE_SUP,
		[COUNTRY] = C.NAME,
		GTD_NUMBER = L.GTD_NUMBER
	FROM INVOICE_OUT_ITEM II
		INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
		INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
		LEFT JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
		LEFT JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
		INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO 
		INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	WHERE II.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL
	ORDER BY GOODS_NAME

	END
	ELSE
	BEGIN
	SELECT
		[GOODS_NAME] = G.NAME,
		[UNIT_NAME] = U.SHORT_NAME,
		[QUANTITY] = II.QUANTITY,
		[PRICE_SAL] = (L.PRICE_SUP / (100 + ISNULL(TT.TAX_RATE, 0))) * 100,
		[SUM_SAL_WITHOUT_VAT] = (L.PRICE_SUP / (100 + ISNULL(TT.TAX_RATE, 0))) * 100 * II.QUANTITY,
		[VAT_SAL] = CASE WHEN ISNULL(TT.TAX_RATE, 0) = 0 THEN CAST('Без НДС' AS VARCHAR(10)) ELSE CAST(ISNULL(TT.TAX_RATE, 0) AS VARCHAR(9)) + '%' END,
		[PSUM_SAL] = II.QUANTITY * ISNULL(TT.TAX_RATE, 0) * L.PRICE_SUP / (100 + ISNULL(TT.TAX_RATE, 0)),
		[SUM_SAL] = II.QUANTITY * L.PRICE_SUP,
		[COUNTRY] = C.NAME,
		GTD_NUMBER = L.GTD_NUMBER
	FROM INVOICE_OUT_ITEM II
		INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
		INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
		LEFT JOIN TAX_TYPE TT ON TT.ID_TAX_TYPE = G.ID_TAX_TYPE
		LEFT JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
		LEFT JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
		INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO 
		INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
	WHERE II.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL
	ORDER BY GOODS_NAME
	END
END
ELSE
BEGIN
SELECT
	[GOODS_NAME] = G.NAME,
	[UNIT_NAME] = U.SHORT_NAME,
	[QUANTITY] = II.QUANTITY,
	[PRICE_SAL] = II.PRICE_SAL - II.PVAT_SAL,
	[SUM_SAL_WITHOUT_VAT] = II.SUM_SAL - II.PSUM_SAL,
	[VAT_SAL] = CASE WHEN II.PSUM_SAL = 0 THEN CAST('Без НДС' AS VARCHAR(10)) ELSE CAST(L.VAT_SAL AS VARCHAR(9)) + '%' END,
	[PSUM_SAL] = II.PSUM_SAL,
	[SUM_SAL] = II.SUM_SAL,
	[COUNTRY] = C.NAME,
	GTD_NUMBER = L.GTD_NUMBER
FROM INVOICE_OUT_ITEM II
	INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
	INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
	LEFT JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
	LEFT JOIN COUNTRY C ON C.ID_COUNTRY = P.ID_COUNTRY
	INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO 
	INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
WHERE II.ID_INVOICE_OUT_GLOBAL = @ID_GLOBAL
ORDER BY GOODS_NAME
END

SELECT
	DIR = DIRECTOR_FIO,
	BUH = BUH_FIO
FROM CONTRACTOR
WHERE ID_CONTRACTOR = (SELECT TOP 1 ID_CONTRACTOR FROM ENTERPRISE_BRANCH WHERE IS_SELF = 1)

RETURN
GO

--EXEC DBO.REPEX_INVOICE_OUT_INVOICE_RIGLA N'<XML><ID_GLOBAL>972D8154-408B-40AD-A550-2F76117D4714</ID_GLOBAL></XML>'