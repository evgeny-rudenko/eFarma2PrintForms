--------------------------------------------------------------------
IF (OBJECT_ID('[REPEX_INVOICE_LS_103]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_INVOICE_LS_103] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_INVOICE_LS_103]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME, @ALL_CONTRACTOR BIT, @ALL_CONTRACTS BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #CONTRACTOR FROM OPENXML(@HDOC, '//ID_CONTRACTOR') 
    WITH(ID_CONTRACTOR BIGINT '.') WHERE ID_CONTRACTOR <> 0
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR = 1 ELSE SET @ALL_CONTRACTOR = 0

    SELECT * INTO #CONTRACTS FROM OPENXML(@HDOC, '//ID_CONTRACTS_GLOBAL') 
    WITH(ID_CONTRACTS_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_CONTRACTS_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTS = 1 ELSE SET @ALL_CONTRACTS = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT 
    NAME_MNN = S.[NAME],
    NAME_TN = G.[NAME],
    QUANTITY_G = SUM(CG.QUANTITY),
    SUM_G = SUM(CG.QUANTITY * CG.PRICE),
    QUANTITY_M = SUM(CM.QUANTITY),
    PRICE_M = CM.SUM_SUP / CM.QUANTITY,
    SUM_M = SUM(CM.SUM_SUP)
FROM CONTRACTS C 
INNER JOIN CONTRACTS_GOODS CG ON CG.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL
INNER JOIN CONTRACTS_MOVEMENT CM ON CM.ID_CONTRACTS_GOODS_GLOBAL = CG.ID_CONTRACTS_GOODS_GLOBAL
INNER JOIN GOODS G ON G.ID_GOODS = CG.ID_GOODS
INNER JOIN SUBSTANCE S ON S.ID_SUBSTANCE = G.ID_SUBSTANCE
INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
WHERE (@ALL_CONTRACTS = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTS WHERE #CONTRACTS.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL))
    AND (@ALL_CONTRACTOR = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTOR WHERE #CONTRACTOR.ID_CONTRACTOR = C.ID_CONTRACTOR))
    AND CM.DATE_OP BETWEEN @DATE_FR AND @DATE_TO
    AND CM.CODE_OP = 'INVOICE'
GROUP BY S.[NAME], G.[NAME], P.[NAME], CM.SUM_SUP/ CM.QUANTITY

RETURN 0
GO
--exec REPEX_INVOICE_LS_103 
--@xmlParam=N'<XML><DATE_FR>2010-04-01T13:20:34.046</DATE_FR><DATE_TO>2010-04-16T13:20:34.046</DATE_TO></XML>'