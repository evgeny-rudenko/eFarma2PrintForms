IF (OBJECT_ID('[REPEX_EXPENSIVE_LS]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_EXPENSIVE_LS] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_EXPENSIVE_LS]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME, @DATE_M_FR DATETIME, @DATE_M_TO DATETIME, @ALL_PROGRAM BIT
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO,
        @DATE_M_FR = DATE_M_FR, @DATE_M_TO = DATE_M_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO',
        DATE_M_FR DATETIME 'DATE_M_FR', DATE_M_TO DATETIME 'DATE_M_TO')

    SELECT * INTO #PROGRAM FROM OPENXML(@HDOC, '//ID_TASK_PROGRAM_GLOBAL') 
    WITH(ID_TASK_PROGRAM_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_TASK_PROGRAM_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_PROGRAM = 1 ELSE SET @ALL_PROGRAM = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

 SELECT  CM.DATE_OP, NUM = CM.DOC_NUM + ' ' + CONVERT(VARCHAR, CM.DATE_OP, 104),
    CM.ID_CONTRACTS_GOODS_GLOBAL, 
    QUANTITY = SUM(CM.QUANTITY / ISNULL(SR.DENOMINATOR, 1)),
    SUMM = SUM(CM.SUM_SUP)
INTO #MON
FROM CONTRACTS_MOVEMENT CM
LEFT JOIN LOT L ON L.ID_LOT_GLOBAL = CM.ID_LOT_GLOBAL
LEFT JOIN STORE ST ON ST.ID_STORE = L.ID_STORE
LEFT JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
WHERE CM.CODE_OP IN ('INVOICE', 'ACT_R2C') AND CM.DATE_OP BETWEEN @DATE_M_FR AND @DATE_TO
GROUP BY CM.ID_CONTRACTS_GOODS_GLOBAL, CM.DATE_OP, CM.DOC_NUM

SELECT 
    NAME_GOODS = G.[NAME],
    QUANTITY = SUM(CG.QUANTITY),
    SUMM = SUM(CG.QUANTITY * CG.PRICE),
    QUANTITY_SUP = SUM(CASE WHEN CC.DATE_OP >= @DATE_FR THEN CC.QUANTITY ELSE 0 END),
    SUM_SUP = SUM(CASE WHEN CC.DATE_OP >= @DATE_FR THEN CC.SUMM ELSE 0 END),
    QUANTITY_MONTH = SUM(CASE WHEN CC.DATE_OP <= @DATE_M_TO THEN CC.QUANTITY ELSE 0 END),
    SUM_MONTH = SUM(CASE WHEN CC.DATE_OP <= @DATE_M_TO THEN CC.SUMM ELSE 0 END),
    NAME_CONTRACTOR = CON.[NAME],
	CC.NUM
FROM CONTRACTS C 
INNER JOIN CONTRACTS_GOODS CG ON CG.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL
INNER JOIN GOODS G ON G.ID_GOODS = CG.ID_GOODS
INNER JOIN CONTRACTOR CON ON CON.ID_CONTRACTOR = C.ID_CONTRACTOR
INNER JOIN #MON CC ON CC.ID_CONTRACTS_GOODS_GLOBAL = CG.ID_CONTRACTS_GOODS_GLOBAL
WHERE (@ALL_PROGRAM = 1 OR EXISTS(SELECT TOP 1 1 FROM #PROGRAM WHERE #PROGRAM.ID_TASK_PROGRAM_GLOBAL = C.ID_TASK_PROGRAM_GLOBAL))
    AND C.TYPE = 'PURCHASE'
GROUP BY G.[NAME], CON.[NAME], CC.NUM

RETURN 0
GO
--exec REPEX_EXPENSIVE_LS @xmlParam=N'<XML><DATE_FR>2009-01-30T15:02:20.437</DATE_FR><DATE_TO>2010-03-30T15:02:20.437</DATE_TO></XML>'


