IF (OBJECT_ID('[REPEX_INVOICE_OUT_MNN]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_INVOICE_OUT_MNN] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_INVOICE_OUT_MNN]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME,
    @ALL_CONTRACTOR BIT, @ALL_STORE BIT, @ALL_GOODS_GROUP BIT, @ALL_MNN BIT
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #CONTRACTOR FROM OPENXML(@HDOC, '//ID_CONTRACTOR') 
    WITH(ID_CONTRACTOR BIGINT '.') WHERE ID_CONTRACTOR IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR = 1 ELSE SET @ALL_CONTRACTOR = 0

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #GOODS_GROUP FROM OPENXML(@HDOC, '//ID_GOODS_GROUP') 
    WITH(ID_GOODS_GROUP BIGINT '.') WHERE ID_GOODS_GROUP IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_GOODS_GROUP = 1 ELSE SET @ALL_GOODS_GROUP = 0

    SELECT * INTO #MNN FROM OPENXML(@HDOC, '//ID_MNN') 
    WITH(ID_MNN BIGINT '.') WHERE ID_MNN IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_MNN = 1 ELSE SET @ALL_MNN = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT 
    SU.ID_SUBSTANCE,
    NAME_MNN = SU.[NAME],
    G.ID_GOODS,
    GOODS_NAME = G.[NAME],
    NAME_UNIT = U.SHORT_NAME,
    QUANTITY = SUM(II.QUANTITY),
    L.PRICE_SUP,
    L.PRICE_SAL
FROM INVOICE_OUT I
INNER JOIN INVOICE_OUT_ITEM II ON II.ID_INVOICE_OUT_GLOBAL = I.ID_INVOICE_OUT_GLOBAL
INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
LEFT JOIN SUBSTANCE SU ON SU.ID_SUBSTANCE = G.ID_SUBSTANCE
WHERE (@ALL_CONTRACTOR = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTOR WHERE #CONTRACTOR.ID_CONTRACTOR = I.ID_CONTRACTOR_TO))
    AND (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = I.ID_STORE))
    AND (@ALL_GOODS_GROUP = 1 OR EXISTS(SELECT TOP 1 1 FROM #GOODS_GROUP 
        INNER JOIN GOODS_2_GROUP G2G ON G2G.ID_GOODS_GROUP = #GOODS_GROUP.ID_GOODS_GROUP
        WHERE G2G.ID_GOODS = G.ID_GOODS))
    AND (@ALL_MNN = 1 OR EXISTS(SELECT TOP 1 1 FROM #MNN WHERE #MNN.ID_MNN = G.ID_SUBSTANCE))
    AND I.DOC_DATE BETWEEN @DATE_FR AND @DATE_TO
GROUP BY SU.ID_SUBSTANCE, SU.[NAME], G.ID_GOODS, G.[NAME], U.[NAME],
    L.PRICE_SUP, L.PRICE_SAL

RETURN 0
GO
--exec REPEX_INVOICE_OUT_MNN @xmlParam=N'<XML></XML>'


