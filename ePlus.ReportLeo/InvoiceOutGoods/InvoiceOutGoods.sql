IF (OBJECT_ID('[REPEX_INVOICE_OUT_GOODS]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_INVOICE_OUT_GOODS] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_INVOICE_OUT_GOODS]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_END DATETIME, @DATE_BEGIN DATETIME, @DATE DATETIME,
    @ALL_CONTRACTOR BIT, @ALL_STORE BIT, @ALL_GOODS_GROUP BIT
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_END = DATE_END, @DATE_BEGIN = DATE_BEGIN, 
        @DATE = [DATE]
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_END DATETIME 'DATE_END', DATE_BEGIN DATETIME 'DATE_BEGIN',
        [DATE] DATETIME 'DATE')

    SELECT * INTO #CONTRACTOR FROM OPENXML(@HDOC, '//ID_CONTRACTOR') 
    WITH(ID_CONTRACTOR BIGINT '.') WHERE ID_CONTRACTOR IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR = 1 ELSE SET @ALL_CONTRACTOR = 0

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #GOODS_GROUP FROM OPENXML(@HDOC, '//ID_GOODS_GROUP') 
    WITH(ID_GOODS_GROUP BIGINT '.') WHERE ID_GOODS_GROUP IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_GOODS_GROUP = 1 ELSE SET @ALL_GOODS_GROUP = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

SELECT
    G.ID_GOODS,
    GOODS_NAME = G.[NAME],
    C_TO.ID_CONTRACTOR,
    CONTRACTOR_NAME = C_TO.[NAME],
    SUM_SAL_MONTH = SUM(CASE WHEN I.DOC_DATE >= @DATE THEN II.SUM_SAL ELSE 0 END),
    QUANTITY_MONTH = SUM(CASE WHEN I.DOC_DATE >= @DATE THEN ROUND(II.QUANTITY * CONVERT(MONEY, SR.NUMERATOR)/SR.DENOMINATOR,2) ELSE 0 END),
    SUM_SAL = SUM(II.SUM_SAL),
    QUANTITY = SUM(ROUND(II.QUANTITY * CONVERT(MONEY, SR.NUMERATOR)/SR.DENOMINATOR,2))
FROM INVOICE_OUT I
INNER JOIN INVOICE_OUT_ITEM II ON II.ID_INVOICE_OUT_GLOBAL = I.ID_INVOICE_OUT_GLOBAL
INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = I.ID_CONTRACTOR_TO
INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
WHERE (@ALL_CONTRACTOR = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTOR WHERE #CONTRACTOR.ID_CONTRACTOR = I.ID_CONTRACTOR_TO))
    AND (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = I.ID_STORE))
    AND (@ALL_GOODS_GROUP = 1 OR EXISTS(SELECT TOP 1 1 FROM #GOODS_GROUP 
        INNER JOIN GOODS_2_GROUP G2G ON G2G.ID_GOODS_GROUP = #GOODS_GROUP.ID_GOODS_GROUP
        WHERE G2G.ID_GOODS = G.ID_GOODS))
    AND I.DOC_DATE BETWEEN @DATE_BEGIN AND @DATE_END
GROUP BY G.ID_GOODS, G.[NAME], C_TO.ID_CONTRACTOR, C_TO.[NAME]

RETURN 0
GO
--exec REPEX_INVOICE_OUT_GOODS @xmlParam=N'<XML></XML>'


