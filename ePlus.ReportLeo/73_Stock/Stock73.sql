IF (OBJECT_ID('[REPEX_STOCK_73]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_STOCK_73] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_STOCK_73]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME, @ALL_STORE BIT, @ALL_PROGRAM BIT, @ALL_CONTRACTS BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE <> 0
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #PROGRAM FROM OPENXML(@HDOC, '//ID_TASK_PROGRAM_GLOBAL') 
    WITH(ID_TASK_PROGRAM_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_TASK_PROGRAM_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_PROGRAM = 1 ELSE SET @ALL_PROGRAM = 0

    SELECT * INTO #CONTRACTS FROM OPENXML(@HDOC, '//ID_CONTRACTS_GLOBAL') 
    WITH(ID_CONTRACTS_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_CONTRACTS_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTS = 1 ELSE SET @ALL_CONTRACTS = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT
    GOODS_NAME = G.[NAME],
    UNIT_NAME = U.SHORT_NAME,
    CG.PRICE,
    QUANTITY = SUM(LM.QUANTITY_ADD - LM.QUANTITY_SUB - LM.QUANTITY_RES),
    PRODUCER_NAME = P.[NAME],
    S.BEST_BEFORE
FROM LOT_MOVEMENT LM
INNER JOIN LOT L ON L.ID_LOT_GLOBAL = LM.ID_LOT_GLOBAL
INNER JOIN CONTRACTS C ON C.ID_CONTRACTS_GLOBAL = L.ID_CONTRACTS_GLOBAL
INNER JOIN CONTRACTS_GOODS CG ON CG.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL
INNER JOIN CONTRACTS_MOVEMENT CM ON CM.ID_CONTRACTS_GOODS_GLOBAL = CG.ID_CONTRACTS_GOODS_GLOBAL
INNER JOIN CONTRACTOR CON ON CON.ID_CONTRACTOR = C.ID_CONTRACTOR
INNER JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
INNER JOIN UNIT U ON U.ID_UNIT = SR.ID_UNIT
INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
INNER JOIN GOODS GG ON GG.ID_GOODS = CG.ID_GOODS
INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
LEFT JOIN SERIES S ON S.ID_SERIES = L.ID_SERIES
WHERE ((CG.CONTROL_TYPE = 'TN' AND G.[NAME] = GG.[NAME]) 
        OR (CG.CONTROL_TYPE != 'TN' AND G.ID_GOODS = GG.ID_GOODS))
    AND (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = L.ID_STORE))
    AND (@ALL_PROGRAM = 1 OR EXISTS(SELECT TOP 1 1 FROM #PROGRAM WHERE #PROGRAM.ID_TASK_PROGRAM_GLOBAL = C.ID_TASK_PROGRAM_GLOBAL))
    AND (@ALL_CONTRACTS = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTS WHERE #CONTRACTS.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL))
    AND LM.DATE_OP BETWEEN @DATE_FR AND @DATE_TO
    AND CM.CODE_OP = 'INVOICE_OUT'
GROUP BY G.[NAME], U.SHORT_NAME, CG.PRICE, P.[NAME], S.BEST_BEFORE

RETURN 0
GO
-exec REPEX_STOCK_73 @xmlParam=N'<XML><DATE_FR>2010-04-01T18:38:49.406</DATE_FR><DATE_TO>2010-04-13T18:38:49.406</DATE_TO></XML>'