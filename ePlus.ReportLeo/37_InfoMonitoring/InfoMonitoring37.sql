IF (OBJECT_ID('[REPEX_INFO_MONITORING_37]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_INFO_MONITORING_37] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_INFO_MONITORING_37]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME, @ALL_STORE BIT, @ALL_CONTRACTOR_GROUP BIT, @ALL_GOODS_GROUP BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE <> 0
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #CONTRACTOR_GROUP FROM OPENXML(@HDOC, '//ID_CONTRACTOR_GROUP') 
    WITH(ID_CONTRACTOR_GROUP BIGINT '.') WHERE ID_CONTRACTOR_GROUP IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR_GROUP = 1 ELSE SET @ALL_CONTRACTOR_GROUP = 0

    SELECT * INTO #GOODS_GROUP FROM OPENXML(@HDOC, '//ID_GOODS_GROUP') 
    WITH(ID_GOODS_GROUP BIGINT '.') WHERE ID_GOODS_GROUP IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_GOODS_GROUP = 1 ELSE SET @ALL_GOODS_GROUP = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT
    MNN = SU.[NAME],
    TN = TN.NAME_RUS,
    DOSAGE = G.DOSAGE,
    CUREFORM = CF.[NAME],
    PRODUCER = P.[NAME],
    QUANITY = SUM(ROUND(II.QUANTITY * CONVERT(MONEY, SR.NUMERATOR) / CONVERT(MONEY, SR.DENOMINATOR), 2)),
    PRICE_SR = SUM(ROUND(II.PRICE_SAL * CONVERT(MONEY, SR.DENOMINATOR) / CONVERT(MONEY, SR.NUMERATOR), 2)) / COUNT(*),
    PRICE_SR_AR = SUM(II.PRICE_SAL * II.QUANTITY) / SUM(ROUND(II.QUANTITY * CONVERT(MONEY, SR.NUMERATOR) / CONVERT(MONEY, SR.DENOMINATOR), 2))
FROM INVOICE_OUT I
INNER JOIN INVOICE_OUT_ITEM II ON II.ID_INVOICE_OUT_GLOBAL = I.ID_INVOICE_OUT_GLOBAL
INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
INNER JOIN SCALING_RATIO SR(NOLOCK) ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
LEFT JOIN SUBSTANCE SU ON SU.ID_SUBSTANCE = G.ID_SUBSTANCE
LEFT JOIN TRADE_NAME TN ON TN.ID_TRADE_NAME = G.ID_TRADE_NAME
LEFT JOIN CURE_FORM CF ON CF.ID_CURE_FORM = G.ID_CURE_FORM
WHERE (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = I.ID_STORE))
    AND (@ALL_CONTRACTOR_GROUP = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTOR_GROUP 
        INNER JOIN CONTRACTOR_2_CONTRACTOR_GROUP C2 ON C2.ID_CONTRACTOR_GROUP = #CONTRACTOR_GROUP.ID_CONTRACTOR_GROUP
        WHERE C2.ID_CONTRACTOR = I.ID_CONTRACTOR_TO))
    AND (@ALL_GOODS_GROUP = 1 OR EXISTS(SELECT TOP 1 1 FROM #GOODS_GROUP 
        INNER JOIN GOODS_2_GROUP G2 ON G2.ID_GOODS_GROUP = #GOODS_GROUP.ID_GOODS_GROUP
        WHERE G2.ID_GOODS = L.ID_GOODS))
    AND I.[DATE] BETWEEN @DATE_FR AND @DATE_TO
    AND I.STATE = 'PROC'
GROUP BY SU.[NAME], TN.NAME_RUS, G.DOSAGE, CF.[NAME], P.[NAME]
ORDER BY SU.[NAME], TN.NAME_RUS

SELECT TOP 1 CONTRACTOR_NAME = [NAME], DIRECTOR_FIO = DIRECTOR_FIO
FROM CONTRACTOR
WHERE ID_CONTRACTOR_GLOBAL = DBO.FN_CONTRACTOR_SELF_GLOBAL()

RETURN 0
GO

--exec REPEX_INFO_MONITORING_37
--    @xmlParam=N'<XML><DATE_FR>2009-08-30 01:45:30.080</DATE_FR><DATE_TO>2010-10-30 18:45:30.080</DATE_TO></XML>'

