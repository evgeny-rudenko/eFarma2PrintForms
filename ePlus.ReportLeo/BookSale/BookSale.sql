IF (OBJECT_ID('[REPEX_BOOK_SALE]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_BOOK_SALE] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_BOOK_SALE]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME, @ALL_STORE BIT, @ALL_PROGRAM BIT, @ALL_CONTRACTS BIT

EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE <> 0
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #PROGRAM FROM OPENXML(@HDOC, '//ID_TASK_PROGRAM_GLOBAL') 
    WITH(ID_TASK_PROGRAM_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_TASK_PROGRAM_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_PROGRAM = 1 ELSE SET @ALL_PROGRAM = 0

    SELECT * INTO #CONTRACTS FROM OPENXML(@HDOC, '//ID_CONTRACTS_GLOBAL') 
    WITH(ID_CONTRACTS_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_CONTRACTS_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTS = 1 ELSE SET @ALL_CONTRACTS = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT
    I.DOC_DATE,
    I.DOC_NUM,
    CONTRACTOR_NAME = C.[NAME],
    CONTRACTOR_TO_NAME = C_TO.[NAME],
    CONTRACTOR_TO_INN = C_TO.INN,
    I.SUM_SAL,
    SUM_VAT_18 = ISNULL((SELECT SUM(D.SVAT_ACC) FROM DOC_MOVEMENT D WHERE D.ID_DOCUMENT = I.ID_INVOICE_OUT_GLOBAL AND D.VAT_RATE_SAL = 18), 0),
    SUM_18 = ISNULL((SELECT SUM(D.SUM_ACC - D.SVAT_ACC) FROM DOC_MOVEMENT D WHERE D.ID_DOCUMENT = I.ID_INVOICE_OUT_GLOBAL AND D.VAT_RATE_SAL = 18), 0),
    SUM_VAT_10 = ISNULL((SELECT SUM(D.SVAT_ACC) FROM DOC_MOVEMENT D WHERE D.ID_DOCUMENT = I.ID_INVOICE_OUT_GLOBAL AND D.VAT_RATE_SAL = 10), 0),
    SUM_10 = ISNULL((SELECT SUM(D.SUM_ACC - D.SVAT_ACC) FROM DOC_MOVEMENT D WHERE D.ID_DOCUMENT = I.ID_INVOICE_OUT_GLOBAL AND D.VAT_RATE_SAL = 10), 0)
FROM INVOICE_OUT I
INNER JOIN CONTRACTS CC ON CC.ID_CONTRACTS_GLOBAL = I.ID_CONTRACTS_GLOBAL
INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = S.ID_CONTRACTOR
INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = I.ID_CONTRACTOR_TO
WHERE (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = I.ID_STORE))
    AND (@ALL_PROGRAM = 1 OR EXISTS(SELECT TOP 1 1 FROM #PROGRAM WHERE #PROGRAM.ID_TASK_PROGRAM_GLOBAL = CC.ID_TASK_PROGRAM_GLOBAL))
    AND (@ALL_CONTRACTS = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTS WHERE #CONTRACTS.ID_CONTRACTS_GLOBAL = I.ID_CONTRACTS_GLOBAL))
    AND I.DOC_DATE BETWEEN @DATE_FR AND @DATE_TO
ORDER BY I.DOC_DATE, I.DOC_NUM, C_TO.[NAME]

RETURN 0
GO
--exec REPEX_BOOK_SALE 
--@xmlParam=N'<XML><DATE_FR>2010-03-01T11:14:34.484</DATE_FR><DATE_TO>2010-03-31T11:14:34.484</DATE_TO><ID_STORE>156</ID_STORE></XML>'

--SELECT * FROM STORE WHERE ID_STORE = 156
