IF (OBJECT_ID('[REPEX_INFO_LS]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_INFO_LS] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_INFO_LS]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME,
    @ALL_CONTRACTS BIT, @ALL_PROGRAM BIT
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #CONTRACTS FROM OPENXML(@HDOC, '//ID_CONTRACTS_GLOBAL') 
    WITH(ID_CONTRACTS_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_CONTRACTS_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTS = 1 ELSE SET @ALL_CONTRACTS = 0

    SELECT * INTO #PROGRAM FROM OPENXML(@HDOC, '//ID_TASK_PROGRAM_GLOBAL') 
    WITH(ID_TASK_PROGRAM_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_TASK_PROGRAM_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_PROGRAM = 1 ELSE SET @ALL_PROGRAM = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT
    NAME_MNN = SU.[NAME],
    NAME_TN = G.[NAME],
    NAME_PRODUCER = P.[NAME],
    G.DOSAGE,
    NAME_CURE_FORM = CF.[NAME],
    SER.BEST_BEFORE,
    II.SUM_SAL,
    II.QUANTITY,
    II.PRICE_SAL
FROM INVOICE_OUT I
INNER JOIN CONTRACTS CC ON CC.ID_CONTRACTS_GLOBAL = I.ID_CONTRACTS_GLOBAL
INNER JOIN INVOICE_OUT_ITEM II ON II.ID_INVOICE_OUT_GLOBAL = I.ID_INVOICE_OUT_GLOBAL
INNER JOIN STORE S ON S.ID_STORE = I.ID_STORE
INNER JOIN CONTRACTOR C ON C.ID_CONTRACTOR = S.ID_CONTRACTOR
INNER JOIN CONTRACTOR C_TO ON C_TO.ID_CONTRACTOR = I.ID_CONTRACTOR_TO
INNER JOIN LOT L ON L.ID_LOT_GLOBAL = II.ID_LOT_GLOBAL
INNER JOIN GOODS G ON G.ID_GOODS = L.ID_GOODS
INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
INNER JOIN SUBSTANCE SU ON SU.ID_SUBSTANCE = G.ID_SUBSTANCE
LEFT JOIN CURE_FORM CF ON CF.ID_CURE_FORM = G.ID_CURE_FORM
LEFT JOIN SERIES SER ON SER.ID_SERIES = L.ID_SERIES
WHERE (@ALL_CONTRACTS = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTS WHERE #CONTRACTS.ID_CONTRACTS_GLOBAL = I.ID_CONTRACTS_GLOBAL))
    AND (@ALL_PROGRAM = 1 OR EXISTS(SELECT TOP 1 1 FROM #PROGRAM WHERE #PROGRAM.ID_TASK_PROGRAM_GLOBAL = CC.ID_TASK_PROGRAM_GLOBAL))
    AND I.DOC_DATE BETWEEN @DATE_FR AND @DATE_TO
    AND CC.TYPE = 'SUPPLY'
ORDER BY SU.[NAME], G.[NAME]

RETURN 0
GO
--exec REPEX_INFO_LS @xmlParam=N'<XML><DATE_FR>2000-01-30T15:02:20.437</DATE_FR><DATE_TO>2010-03-30T15:02:20.437</DATE_TO></XML>'


