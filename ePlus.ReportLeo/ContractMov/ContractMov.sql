IF (OBJECT_ID('[REPEX_CONTRACT_MOV]') IS NULL) EXEC ('CREATE PROCEDURE [REPEX_CONTRACT_MOV] AS RETURN')
GO
ALTER PROCEDURE [dbo].[REPEX_CONTRACT_MOV]
    @XMLPARAM NTEXT
AS
DECLARE @HDOC INT
DECLARE @DATE_FR DATETIME, @DATE_TO DATETIME,
    @ALL_SUPPLIER BIT, @ALL_STORE BIT, @ALL_CONTRACTOR BIT, @ALL_CONTRACTS BIT
EXEC SP_XML_PREPAREDOCUMENT @HDOC OUT, @XMLPARAM

    SELECT TOP 1 @DATE_FR = DATE_FR, @DATE_TO = DATE_TO
    FROM OPENXML(@HDOC, '/XML') WITH(DATE_FR DATETIME 'DATE_FR', DATE_TO DATETIME 'DATE_TO')

    SELECT * INTO #SUPPLIER FROM OPENXML(@HDOC, '//ID_SUPPLIER') 
    WITH(ID_SUPPLIER BIGINT '.') WHERE ID_SUPPLIER <> 0
    IF @@ROWCOUNT = 0 SET @ALL_SUPPLIER = 1 ELSE SET @ALL_SUPPLIER = 0

    SELECT * INTO #STORE FROM OPENXML(@HDOC, '//ID_STORE') 
    WITH(ID_STORE BIGINT '.') WHERE ID_STORE <> 0
    IF @@ROWCOUNT = 0 SET @ALL_STORE = 1 ELSE SET @ALL_STORE = 0

    SELECT * INTO #CONTRACTOR FROM OPENXML(@HDOC, '//ID_CONTRACTOR') 
    WITH(ID_CONTRACTOR BIGINT '.') WHERE ID_CONTRACTOR <> 0
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTOR = 1 ELSE SET @ALL_CONTRACTOR = 0

    SELECT * INTO #CONTRACTS FROM OPENXML(@HDOC, '//ID_CONTRACTS_GLOBAL') 
    WITH(ID_CONTRACTS_GLOBAL UNIQUEIDENTIFIER '.') WHERE ID_CONTRACTS_GLOBAL IS NOT NULL
    IF @@ROWCOUNT = 0 SET @ALL_CONTRACTS = 1 ELSE SET @ALL_CONTRACTS = 0

EXEC SP_XML_REMOVEDOCUMENT @HDOC

EXEC DBO.USP_RANGE_NORM @DATE_FR OUT, @DATE_TO OUT

SELECT
    C.ID_CONTRACTS_GLOBAL,
    NAME_CONTRACTS = C.CONTRACTOR_CODE,
    NAME_MNN = S.[NAME],
    NAME_TN = TN.NAME_RUS,
    NAME_PRODUCER = P.[NAME],
    CG.PRICE,
    CG.QUANTITY,
    SUMM = CG.PRICE * CG.QUANTITY,
    PRICE_SUP = CC.SUM_SUP / CC.QUANTITY_SUP,
    QUANTITY_SUP = CC.QUANTITY_SUP,
    SUM_SUP = CC.SUM_SUP
FROM CONTRACTS C 
INNER JOIN CONTRACTS_GOODS CG ON CG.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL
INNER JOIN GOODS G ON G.ID_GOODS = CG.ID_GOODS
INNER JOIN SUBSTANCE S ON S.ID_SUBSTANCE = G.ID_SUBSTANCE
INNER JOIN PRODUCER P ON P.ID_PRODUCER = G.ID_PRODUCER
INNER JOIN (
    SELECT 
        CM.ID_CONTRACTS_GOODS_GLOBAL, 
        QUANTITY_SUP = SUM(CM.QUANTITY / ISNULL(SR.DENOMINATOR, 1)),
        SUM_SUP = SUM(CM.SUM_SUP)
    FROM CONTRACTS_MOVEMENT CM
    LEFT JOIN LOT L ON L.ID_LOT_GLOBAL = CM.ID_LOT_GLOBAL
    LEFT JOIN STORE ST ON ST.ID_STORE = L.ID_STORE
    LEFT JOIN SCALING_RATIO SR ON SR.ID_SCALING_RATIO = L.ID_SCALING_RATIO
    WHERE CM.CODE_OP IN ('INVOICE', 'ACT_R2C') AND CM.DATE_OP BETWEEN @DATE_FR AND @DATE_TO
        AND (@ALL_SUPPLIER = 1 OR EXISTS(SELECT TOP 1 1 FROM #SUPPLIER WHERE #SUPPLIER.ID_SUPPLIER = ST.ID_CONTRACTOR OR ST.ID_CONTRACTOR IS NULL))
        AND (@ALL_STORE = 1 OR EXISTS(SELECT TOP 1 1 FROM #STORE WHERE #STORE.ID_STORE = L.ID_STORE OR L.ID_STORE IS NULL))
    GROUP BY CM.ID_CONTRACTS_GOODS_GLOBAL
    ) CC ON CC.ID_CONTRACTS_GOODS_GLOBAL = CG.ID_CONTRACTS_GOODS_GLOBAL
LEFT JOIN TRADE_NAME TN ON TN.ID_TRADE_NAME = G.ID_TRADE_NAME
WHERE (@ALL_CONTRACTOR = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTOR WHERE #CONTRACTOR.ID_CONTRACTOR = C.ID_CONTRACTOR)) 
    AND (@ALL_CONTRACTS = 1 OR EXISTS(SELECT TOP 1 1 FROM #CONTRACTS WHERE #CONTRACTS.ID_CONTRACTS_GLOBAL = C.ID_CONTRACTS_GLOBAL))
ORDER BY C.CONTRACTOR_CODE, S.[NAME], TN.NAME_RUS

RETURN 0
GO
--exec REPEX_CONTRACT_MOV @xmlParam=N'<XML><DATE_FR>2010-01-30T15:02:20.437</DATE_FR><DATE_TO>2010-03-30T15:02:20.437</DATE_TO></XML>'